/*E*/
/* Fichier : $Id: ITMA_TPM_pilotage_d_un_PMV.am,v 1.117 2020/11/03 17:46:22 pc2dpdy Exp $        $Revision: 1.117 $        $Date: 2020/11/03 17:46:22 $
*-------------------------------------------------------------------------------
* GTIE *  PROJET MIGRAZUR
-------------------------------------------------------------------------------
* SOUS-SYSTEME  GESIHM
-------------------------------------------------------------------------------
* MODULE MTPM * FICHIER ITMA_TPM_pilotage_d_un_PMV.am
-------------------------------------------------------------------------------
* DESCRIPTION DU MODULE :
*
* Le module permet de commander l'affichage sur 1 PMV 3 ou 4 lignes
*
-------------------------------------------------------------------------------
* HISTORIQUE :
*
* Mismer 	23 Aout 1996 : Creation								1.1
* Mismer 	12 Sept 1996 : Ajout modif pour pilotage au PC simplifié	1.2
* Mismer 	27 Fev  1997 : Ajout modif pour pilotage TDP (DEM/1417)	1.85
* Mismer 	13 Mar  1997 : Ajout modif pour pilotage TDPsuite (DEM/1417)1.86
* Mismer 	05 Jun  1997 : Supp alternat sur changement PMV (DEM/1448)1.87
* Mismer  20 Jun  1997  : Autorisation modif ligne 1 TDP (DEM/1452) 1.88
* Guilhou 03 oct 1997 : liste des FMC cause a le m look que celle des FMC a traiter (ana/65) 1.89
* Niepceron 05 fev 1997 : Correction dans ITMA_TPM_Liste_Eqt_Dispo v1.91
* Guilhou	03 fev 1988 : ajout sit_origine dans XZAC01 (dem/1605) 1.93
* Mismer  30 Mai 2000 : interdiction affichage temperature  1.95
* Hilmarcher
* Niepceron  29/04/2005: modif tdp: affichage 2 tdp en neutre DEM464
* Niepceron    	1.97		27/05/2005	: ajout de trace + création d'action s'il y a affichage picto+TDP  DEM464
* Niepceron	1.98		21/10/2005	: Suppression de vm_District=11 en dur DEM464
* Niepceron	1.99		21/11/2005	: Possibilité d'afficher du TDP sur tous les PMV DEM464
* Niepceron	1.100		23/08/2006	: Ajout de la mise à jour de vm_Lect_PMV.AltDestination = vl_NumDest[1,0] sur action de BL_PMV DEM582
* JMG   31/10/07        : ajout site de gestion 1.101
* JPL	02/04/09	: Ajout du bouton 'copier/coller' etat actuel 1.102
* JMG   29/09/09        : SECTO DEM 887
* JPL	21/09/10	: Traces plus precises ; fonctionnement identique a la precedente 1.104
* JPL	21/09/10	: Multi-pilotage de PMVs simultanement (DEM 945) 1.105
* JMG	05/12/11	: linux
* LCL	12/12/11	: Ajout TdP 3ieme destination (DEM 1014) 1.106
* JMG	04/07/12	: pictos vitesse 1.107
* JPL	14/03/13	: Prise en compte mise au neutre programme (DEM 1061)  1.108
* JPL	28/03/13	: Forcage type commande a Texte sur modif. nouveau message validee  1.109
* PNI	03/03/15	: Suppression de la mise en majuscule des messages
* JPL	23/03/17	: Changement du terme Secteur en Region (DEM 1173)  1.111
* JPL	28/08/17	: Masquage de l'alternat demande sur choix d'affichage de Temps de parcours  1.112
* JPL	29/08/17	: Prise en compte du mode d'affichage (clignotant / alterne) programme (DEM 1230)  1.113
* JMG	30/10/17	: cligno rapide -> lent 1.114
* JMG	01/08/18	: differents types de PMV suppression SAGA DEM1306 1.115
* JPL	09/01/19	: Données de mise au neutre d'un panneau à l'instant courant (DEM 1312)  1.116
* LCL   22/04/20        : MOVIS Ajout site local pilotage DEM-SAE93
---------------------------------------------------------------------------- */


/*A Description des constantes générales
 * ------------------------------------*/
DEFINE	C_MODULE			"MPMV"	' Nom du module

DEFINE	REP_CONFIG		"../fichiers/"
DEFINE	CM_4LIGNES		4

/*A Description des modules à inclures
 * ----------------------------------
 */
INCLUDE	"dbase_.am"

INCLUDE	"XDMICG/inc/xdc_ax.h"
INCLUDE	"XDMICG/inc/xzic_ax.h"

INCLUDE	"GESIHM/inc/ITMA_COM.h"
INCLUDE	"GESIHM/inc/ITMA_TDO.h"
INCLUDE	"GESIHM/inc/ITMA_TAR.h"
INCLUDE	"GESIHM/inc/ITMA_PMV_format.h"
INCLUDE	"GESIHM/inc/ITMA_TPM_pilotage_PMV.h"


/*A Description des procedures sotckées à appeler
 * ---------------------------------------------*/
INCLUDE	"GESIHM/inc/xzac01sp.h"
INCLUDE	"GESIHM/inc/xzac40sp.h"
INCLUDE	"GESIHM/inc/xzac61sp.h"
INCLUDE "GESIHM/inc/xzac611sp.h"
INCLUDE "GESIHM/inc/xzac610sp.h"
INCLUDE	"GESIHM/inc/xzat01sp.h"
INCLUDE	"GESIHM/inc/xzat05sp.h"
INCLUDE	"GESIHM/inc/xzat63sp.h"
INCLUDE "GESIHM/inc/xzao427sp.h"


/*A Description des variables globales
 * ----------------------------------*/
var     	vm_fenetre			' item de la fenetre du module
var     	vm_objet_selecte		' L'objet courant de la fenetre
var     	vm_la_fenetre_est_active	' flag d'activation de la fenetre

var		vm_titre_table_picto	' titre du tableau des picto.
var 		vm_table_picto			' libellés du tableau des pictogrammes
var 		vm_pix_table_picto		' liste des fichiers de picto 20*20
var		vm_pix_table_picto_big	' liste des fichiers de picto 40*40

var		vm_titre_table_proposition	' titre du tableau des propositions.
var 		vm_table_proposition	' libellés du tableau des proposition
var 		vm_pix_table_proposition	' liste des picto 20*20 du tableau proposition
var		vm_liste_elements		' liste des diférents éléments
var		vm_liste_PMV			' liste des différents PMV
var		vm_liste_picto			' la liste des pictogrammes disponibles
var		vm_Text_Actif			' type du text modifié (message ou alternat)
var		vm_picto				' la liste des pictos (petit,grand,symbole)
var		vm_District			' le code du district
var		vm_NomSite			' le nom du district
var		vm_les_districts		' Liste des district
var		vm_NomOperateur
var		vm_validation_nouv_message ' flag de validation d'une modif de message 
var		vm_mode_formation		' Mode Formation
var		vm_Appel
var		vm_type_Picto			' liste de types de Picto
var             vm_poke_tdp1                    ' tdp pour destination 1
var             vm_poke_tdp2                    ' tdp pour destination 2
var             vm_poke_tdp3                    ' tdp pour destination 3
var             vm_Destination                  ' Destinations tdp

var format Config_PMV		vm_Config_PMV	' table avec t[n] = configuration du PMV numéro n
var format type_PMV_GEN		vm_PMV		' PMV dont l'état actuel est affiché
var format type_PMV_GEN		vm_Lect_PMV	' PMV séléctionné dans la liste pour affichage nouveau message


/*A Description des tableaux
 * ------------------------*/
var format type_PMV_GEN vm_etat_PMV		' table avec t[n] = état du PMV numéro n

/*X*/
/* ----------------------------------------------------------------------------
* 	DESCRIPTION DES DIFFERENTS MACROS LIEES AU MODULE
*		'ITMA_TPM_pilotage_d_un_PMV.am' :
*  ----------------------------------------------------------------------------
*
*	ITMA_TPM_pilotage_d_un_PMV (va_appelant,va_1,va_2,va_3) 
*		description :	Commande l'affichage sur PMV via IHM
*
* 	ITMA_TPM_BL_éléments_click
*		description :	Capture le click de la liste des éléments
*					MAJ des lignes 1,2 et 3
*
* 	ITMA_TPM_Tabuler_ligne (old,new,taille)
*		description :	Centre un message sur une zone de 'n' caractère
*
*	ITMA_TPM_Modif_message
*		description :	Modifications du message et MAJ de la BL
*
*	ITMA_TPM_Appel_Modif_message 
*		description :	Affichage d'un BE pour modification du message
*
*	ITMA_TPM_Lecture_PMV 
*		descritpion :	Lecture des PMV disponibles
*					via 'XZAT05_Utilisation_PMV'
*
*	ITMA_TPM_MAJ_PMV ( va_type )
*		description :	MAJ des données nouvelle  ou actuel sur l'IHM
*					depuis la table PMV
*	ITMA_TPM_Commande_PMV 
*		description :	Commande du nouveau PMV
*
*	ITMA_TPM_Liste_Eqt_Dispo 
*		description :	Lecture des PMV disponible
*
*	ITMA_TPM_Executer_PMV 
*		description :	Execution des commandes PMV
*
*  ----------------------------------------------------------------------------
*/


/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Commande l'affichage sur PMV 
*
*  ----------------------------------------------------------------------------
* SEQUENCE D'APPEL :
*/

MACRO ITMA_TPM_pilotage_d_un_PMV (va_appelant,va_1,va_2,va_3,va_4,va_5,va_6)

/*
* ARGUMENTS EN ENTREE :
*  va_appelant		: Nom de la macro Applix ayant invoque la presente.
*  va_1 à va_6		: N° d'ordre + N° PA  ou  N° Equipement
*
*
* ARGUMENTS EN SORTIE : Aucun
*
*
* CODE RETOUR         : Indefini
*
* CONDITION D'UTILISATION
*   Selection de Piloter -> PMV dans MTPA : Plan d'action
*   Selection de Piloter -> PMV dans MTMT : menu textuel
*  Selection d'un PMV sur synotique ( clic droit) -> ISYN_SOP
*   Selection d'un PMV sur synotique ( clic gauche ) -> ITMA_TUE
*
* FONCTION
*   Lit en base de donnees puis affiche la liste des alarmes en cours en
*   mettant en evidence les changements par rapport a la derniere consultation.
*   Permet a l'operateur de choisir des alarmes dans la liste affichee puis
*   d'en effectuer l'acquittement, avec mise a jour de la base de donnees.
*
---------------------------------------------------------------------------- */


var		i,j,k				' index de loop
var		vl_mess
var		vl_liste_font			' liste des fonts disponibles sous Applix
var		vl_objet				' objet en cours de modification
var		vl_taille				' taille d'une chaine de caractères
var		vl_tampon				' table temporaire
var		vl_retour		 		' code retourne à l'appelant
var		vl_lib_fmc			' tableau du libellé fmc
var		vl_fmc				' libellé fmc à construire
var		vl_fmc1				' 1ere partie du libellé fmc
var		vl_fmc2				' 2eme partie du libellé fmc
var		vl_erreur				' flag d'erreur
var		vl_numero
var		tl_titres,tl_data,tl_retour
var		vl_ligne
var		vl_rang
var		vl_lecture
var		vl_message
var		vl_trouve
var		vl_tplecture			' text tampon de lecture d'une liste
var		vl_tptext				' zone de text tampon
var		vl_messages_acceptes	' les messages acceptés en "_poke"
var		vl_coderet
var		tl_liste_causes
var		vl_Destination
var		vl_NumDest
var             vl_NumDesttdp
var		vl_num_eqt
var		vl_liste_district
var		tl_sel_pmv
VAR FORMAT SQL_Procedure_Params@ vl_parametres
VAR FORMAT SQL_Procedure_Result@ vl_resultats


vl_retour 	= { COM_QUITTE }
vl_liste_font 	= LIST_FONT_FAMILIES@()

/*vm_NomSite 	= SYSTEM_VAR@("vg_nom_site")*/
vm_District 	= SYSTEM_VAR@("vg_site")

vm_NomOperateur = SYSTEM_VAR@(vg_operateur)
vm_NomOperateur = SUBSTRING@(vm_NomOperateur, 1, 25)
vm_Config_PMV 	= SYSTEM_VAR@(vg_type_PMV)
vm_type_Picto 	= SYSTEM_VAR@(vg_type_Picto)
vm_validation_nouv_message = XDC_FAUX

/*!!! */
/*!!!  SET_SYSTEM_VAR@(vg_formation,COM_FORMATION_PASSIF) */
/*!!! */

vm_mode_formation = (SYSTEM_VAR@(vg_formation) <> NULL)


/*A Definition du traitement a realiser en cas d'erreur
* -----------------------------------------------------*/

/*
ON ERROR {
    ERROR_BOX@
    COM01_Trace (COM_WARNING, COM03_Message_Erreur (C_MODULE))
    RETURN
}
*/

vl_tptext = "------------------------------------------------------------"
COM01_Trace(COM_INFO,vl_tptext)
vl_tptext = "Start ITMA_TPM_pilotage_d_un_PMV " ++ "   User : " ++ vm_NomOperateur
COM01_Trace(COM_INFO,vl_tptext)

vl_tptext = "va_appelant : " ++ va_appelant
COM01_Trace(0,vl_tptext)

if NOT(IS_ARRAY@(va_1))
{vl_tptext = "va_1 : " ++ va_1
COM01_Trace(0,vl_tptext)}

if NOT(IS_ARRAY@(va_2))
{vl_tptext = "va_2 : " ++ va_2
COM01_Trace(0,vl_tptext)}

if NOT(IS_ARRAY@(va_3))
{vl_tptext = "va_3 : " ++ va_3
COM01_Trace(0,vl_tptext)}

if NOT(IS_ARRAY@(va_4))
{vl_tptext = "va_4 : " ++ va_4
COM01_Trace(0,vl_tptext)}

if NOT(IS_ARRAY@(va_5))
{vl_tptext = "va_5 : " ++ va_5
COM01_Trace(0,vl_tptext)}

if NOT(IS_ARRAY@(va_6))
{vl_tptext = "va_6 : " ++ va_6
COM01_Trace(0,vl_tptext)}

vm_Appel = SUBSTRING@(va_appelant,1,8)


/*A test les droits d'acces à cette fonction */
if (vm_appel="ISYN_SOP")
{
	if NOT vm_mode_formation  and NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_DISTRICT)) and 
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_PC_SIMPLIFIE)) and
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_PC2)) and 	
	(NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_CI) and
	  SYSTEM_VAR@(vg_site) = XDC_CI) )
	{		
		vl_tptext = " Appel pilotage PMV depuis syno par un utilisateur non autorisé"
		COM01_Trace(COM_WARNING,vl_tptext)
		RETURN(COM_OK)
	}
}
/*A Définition de la bibliothèque des pictogrammes
* à l'aide du fichier 'pmv/Config_Pictogrammes.cfg' 
* ----------------------------------------------------*/

if FILE_EXISTS@(REP_CONFIG ++ "pmv/Config_Pictogrammes.cfg")
{	vm_table_picto = READ_ASCII_FILE@(REP_CONFIG ++ "pmv/Config_Pictogrammes.cfg") 
	vl_taille = ARRAY_SIZE@(vm_table_picto) - 1
	FOR i = 1 TO vl_taille
		vl_tampon 	= COMMA_SPLIT@(vm_table_picto[i])
		vm_picto[vl_tampon[0],0]	= 
				REP_CONFIG ++ 
				"pic/" ++ TRIM@(TABS_TO_SPACES@(vl_tampon[1]))  ' fichiers 20*20
		vm_picto[vl_tampon[0],1]	= 
				REP_CONFIG ++ "pic/" ++ 
				TRIM@(TABS_TO_SPACES@(vl_tampon[2]))  ' fichiers 40*40
		vm_picto[vl_tampon[0],2]	= 
				TRIM@(TABS_TO_SPACES@(vl_tampon[3]))  ' trigram
		vm_picto[vl_tampon[0],3]	= 
				TRIM@(TABS_TO_SPACES@(vl_tampon[4]))  ' libellé
	NEXT i
}
else
{
	info_message@("Le fichier '"++ 
			REP_CONFIG ++
			"pmv/Config_Pictogrammes.cfg' est manquant, les pictogrammes ne seront pas visibles !")
	for i = 1 to 21
		vm_picto[i,0]	= REP_CONFIG ++ "pic/pix_panneau_eteint"
		vm_picto[i,1]	= REP_CONFIG ++ "pic/pix_panneau_eteint_40_40"
		vm_picto[i,3]	= "XXXX"
		vm_picto[i,4]	= "Panneau eteint"
	next i
}



vm_liste_PMV = ITMA_TPM_Liste_Eqt_Dispo ()

if NOT IS_ARRAY@(vm_liste_PMV)
{ 
	INFO_MESSAGE@( "Aucun PMV disponible" )
	RETURN(COM_OK)
}

if vm_appel="ISYN_SOP"
{
	if IS_ARRAY@(va_1) { vl_numero = va_1[0]+0 }
	else { vl_numero = va_1+0 }

	/* Au CI si appel depuis depuis le synoptique prendre le district du PMV  */
	if vm_District = XDC_CI
	{ 
		if NOT ITMA_COM_District_pilotable(vm_Config_PMV[vl_numero].sitegestion, 
				XDC_LIB_PMV,TRUE,vm_Config_PMV[vl_numero].Identifiant) 
		{ 
			vl_tptext = " Appel pilotage PMV depuis syno pour un PMV pilotable en région " ++ vm_Config_PMV[vl_numero].NomSite
			COM01_Trace(COM_INFO,vl_tptext)
				RETURN(COM_OK)
 		}
	}
	else
	{ 
		if (vm_District <> vm_Config_PMV[vl_numero].sitegestion) 
		{ 
			vl_tptext = " Appel pilotage PMV depuis syno pour un PMV hors région " ++ vm_Config_PMV[vl_numero].NomSite
			COM01_Trace(COM_INFO,vl_tptext)
			RETURN(COM_OK)
		}
	}
	

	vm_Lect_PMV 	= vm_etat_PMV[vl_numero]
	vm_PMV 		= vm_etat_PMV[vl_numero]

	vl_numero = ARRAY_INDEX@(vm_liste_PMV,vm_PMV.Identifiant)
	if vl_numero = -1
	{ 
		INFO_MESSAGE@( "PMV indisponible" )
		RETURN(COM_OK)
	}

}
else
{

	vl_numero = ARRAY_INDEX@(ARRAY_COLUMN@(vm_Config_PMV,1),vm_liste_PMV[0])
	if vl_numero >= 0
 	{ 
		vm_Lect_PMV 	= vm_etat_PMV[vl_numero]
		vm_PMV 		= vm_etat_PMV[vl_numero]
	}
}


/*A Charger la fenetre de pilotage d'un PMV 
 * ----------------------*/
vm_fenetre = DB_LOAD@("ITMA_TPM_pilotage_d_un_PMV")
vl_messages_acceptes[0] = COM_CANAL_FIN
vl_messages_acceptes[1] = COM_CANAL_MTPM_ETAT
vl_messages_acceptes[2] = COM_CANAL_MTPM_PICTO
vl_messages_acceptes[3] = COM_CANAL_MTPM_TDP
DB_ACCEPT_POKES@ (vm_fenetre, vl_messages_acceptes)
DB_DISPLAY_ONLY@(vm_fenetre,TRUE)
DB_WINDOW_REMAIN@ (vm_fenetre, TRUE)
DB_XPOS@ (vm_fenetre,0)
if (GET_ENV_VAR@("RTARCH")="hp700_hpux")
DB_YPOS@ (vm_fenetre,300)
else
DB_YPOS@ (vm_fenetre,104)
DB_CTRL_HORIZ_SCROLL@(vm_fenetre,"TA_pictogrammes",FALSE)
DB_CTRL_HORIZ_SCROLL@(vm_fenetre,"TA_proposition",FALSE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"TA_pictogrammes",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"TA_proposition",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BO_Arret",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BA_clignotement1_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BA_clignotement2_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BA_clignotement3_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BA_clignotement_picto",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_message_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_alternat_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_direction_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_alt_dir_nouveau",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_PMV",TRUE)
DB_CTRL_MULTI_SELECT@(vm_fenetre,"BL_PMV",TRUE)
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_éléments",TRUE)


/*A Definir les caracteristiques (initiales) des objets graphiques
* --------------------------------------------------------------*/

/* effacer certains éléments de la boite de dialogue */
DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",FALSE)
DB_CTRL_GRAYED@(vm_fenetre,"BP_lier_fmc",FALSE)

/* afficher sur fond blanc les zones de listes et de tableaux */
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_message_actuel",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_message_nouveau",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_alternat_actuel",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_alternat_nouveau",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_direction_actuel",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_direction_nouveau",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_alt_dir_actuel",TRUE)
DB_CTRL_WORK_COLORS@(vm_fenetre,"BL_alt_dir_nouveau",TRUE)
/*
DB_CTRL_WORK_COLORS@(vm_fenetre,"BE_remarque",TRUE)
*/


/*A Affichage de la fenetre
* -------------------------*/
DB_DISPLAY@(vm_fenetre)


/*A MAJ TA_pictogrammes
* -------------------*/
vm_pix_table_picto 		= ""
vm_pix_table_picto[0]	= REP_CONFIG ++ "pic/pix_panneau_eteint",""
vm_table_picto 		= ""
vm_table_picto[0,0]		=""
vm_titre_table_picto 	= ""
vm_titre_table_picto[0,0] = "Libellé"
vm_titre_table_picto[0,1] = 150

DB_TABLE_SET_MARKER_WIDTH@(vm_fenetre,"TA_pictogrammes",24)
DB_TABLE_SET_DATA@(vm_fenetre,"TA_pictogrammes",vm_table_picto,vm_titre_table_picto)
DB_TABLE_MARKER_PIXMAPS@(vm_fenetre,"TA_pictogrammes",0,vm_pix_table_picto)  		


/*A MAJ TA_proposition
* -------------------*/
vm_pix_table_proposition 		= ""
vm_pix_table_proposition[0]		= REP_CONFIG ++ "/pic/pix_panneau",""
vm_table_proposition 			= ""
vm_table_proposition[0,0]		=" "
vm_table_proposition[0,1]		=" "
vm_table_proposition[0,2]		=" "
vm_table_proposition[0,3]		=" "
vm_titre_table_proposition 		= ""
vm_titre_table_proposition[0,0] 	= "Lib.Picto"
vm_titre_table_proposition[0,1]	= 70
vm_titre_table_proposition[1,0] 	= "Ligne 1"
vm_titre_table_proposition[1,1]	= 160
vm_titre_table_proposition[2,0] 	= "Ligne 2"
vm_titre_table_proposition[2,1]	= 160
vm_titre_table_proposition[3,0] 	= "Ligne 3"
vm_titre_table_proposition[3,1]	= 160


DB_TABLE_ALLOW_EDITING@(vm_fenetre,"TA_proposition",True)
DB_TABLE_ALLOW_COLUMN_RESIZING@(vm_fenetre,"TA_proposition",True)
DB_TABLE_SET_MARKER_WIDTH@(vm_fenetre,"TA_proposition",24)
DB_TABLE_SET_DATA@(	vm_fenetre,
				"TA_proposition",
				vm_table_proposition,
				vm_titre_table_proposition)
DB_TABLE_MARKER_PIXMAPS@(vm_fenetre,"TA_proposition",0,vm_pix_table_proposition)  		




vm_liste_elements = READ_ASCII_FILE@(REP_CONFIG ++ "pmv/ConfigListeElements.cfg")

DB_CTRL_STRINGS@(vm_fenetre, "BL_éléments", vm_liste_elements)
DB_CTRL_VALUE@(vm_fenetre, "BL_éléments", 0)
DB_CTRL_PICK_DEFAULT@(vm_fenetre, "BL_éléments", TRUE)

DB_CTRL_STRINGS@(vm_fenetre,"BL_PMV",vm_liste_PMV)
vl_numero = ARRAY_INDEX@(vm_liste_PMV,vm_PMV.Identifiant)
tl_sel_pmv = { vl_numero }
DB_CTRL_VALUE@(vm_fenetre, "BL_PMV", tl_sel_pmv)	

/*A Modif. de la police des fenetres de messages et d'alternats
* -----------------------------------------------------------*/
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_message_actuel",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_alternat_actuel",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_direction_actuel",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_alt_dir_actuel",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_message_nouveau",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_alternat_nouveau",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_direction_nouveau",TRUE)
DB_CTRL_MONOSPACE@(vm_fenetre,"BL_alt_dir_nouveau",TRUE)


/*A Lire l'état actuel des PMV selon l'origine de l'appel
 * ------------------------------------------------------*/


vl_erreur = ITMA_TPM_Lecture_PMV()
if vl_erreur<>COM_NOK then ITMA_TPM_MAJ_PMV  ( "actuel" )
ITMA_TPM_MAJ_table_picto 
ITMA_TPM_MAJ_PMV  ( "nouveau" )
DB_CTRL_VALUE@(vm_fenetre,"BO_Arret",1)


/*A MAJ de la liste des lignes de message
* -------------------------------------*/
 ITMA_TPM_BL_elements_click

/*A flag de capture des évenements dans la fenetre
* ------------------------------------------------*/
DB_DISPLAY_ONLY@(vm_fenetre,FALSE)


DB_CTRL_GRAYED@(vm_fenetre,"BP_message",FALSE)
DB_CTRL_GRAYED@(vm_fenetre,"BP_alternat",FALSE)
/*DB_CTRL_DISPLAY@(vm_fenetre,"BP_explication",TRUE)
DB_CTRL_DISPLAY@(vm_fenetre,"BP_refuser",TRUE)*/
DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination",FALSE)
DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination_alt",FALSE)


/*A Abonnement a l'etat des panneaux */ 
if (SYSTEM_VAR@(vg_site) = XDC_CI) 
{
	vl_liste_district = SYSTEM_VAR@ ( vg_les_districts )
	for i = 0 to ARRAY_SIZE@(vl_liste_district) - 1
		if ( vl_liste_district[i][3] = XDC_TYPEM_PC2 ) or 
		   ( vl_liste_district[i][3] = XDC_TYPEM_PC2LT ) or 
	 	   ( vl_liste_district[i][3] = XDC_TYPEM_PCS )

		{
			ITMA_TPM_Abt_Etat_PMV(vl_liste_district[i][2])
		}
	next i
}
else		
{
	ITMA_TPM_Abt_Etat_PMV(SYSTEM_VAR@ (vg_nom_site))
}




/*A Gestion des événements liés à la fiche 'Pilotage des PMV'
* --------------------------------------------------------*/
vm_la_fenetre_est_active = TRUE
WHILE (vm_la_fenetre_est_active)

	/*A
	 * Lecture de la date system et MAJ du libellé
	 * -------------------------------------------
	 */
	DB_DISPLAY@ (vm_fenetre)

    	vm_objet_selecte = DB_EXIT_CTRL@ (vm_fenetre)
    	IF SYSTEM_VAR@ (vg_verrou) vm_objet_selecte = NULL

	CASE OF vm_objet_selecte

	/*A SI action sur bouton QUITTER */
	CASE "BP_quitter"

		vm_la_fenetre_est_active = FALSE 


 	/*B appui sur le bouton lier fmc*/
	CASE "BP_lier_fmc"

		vl_tptext = "Status : appel lien FMC"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		/*B on lance la tache de recherche d'evt cause*/
		tl_data=PEND_FOR_NEW_TASK@("ITMA_COM_Liste_Causes",
							"ITMA_TPM_pilotage_d_un_PMV")

		tl_liste_causes=tl_data[0]
		/*B affiche la liste des evts a traiter*/
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_Evenements",TRUE)
		DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"TA_Evenements",TRUE)
		DB_TABLE_ALLOW_COLUMN_RESIZING@(vm_fenetre,"TA_Evenements",True)
		DB_TABLE_SET_MARKER_WIDTH@(vm_fenetre, "TA_Evenements", 25)
		tl_titres[0] = COM_CAUSE_NUMERO,COM_CAUSE_LONG_NUMERO
		tl_titres[1] = COM_CAUSE_CLE,COM_CAUSE_LONG_CLE
		tl_titres[2] = COM_CAUSE_TYPE,COM_CAUSE_LONG_TYPE
		tl_titres[3] = COM_CAUSE_DATE,COM_CAUSE_LONG_DATE
		tl_titres[4] = COM_CAUSE_LOCALISATION,COM_CAUSE_LONG_LOCALISATION
		DB_TABLE_SET_DATA@(vm_fenetre,"TA_Evenements",tl_data[1],tl_titres)
		DB_TABLE_MARKER_PIXMAPS@(vm_fenetre, "TA_Evenements", 0, tl_data[2])
		
 	/*B appui sur le bouton destination */
	CASE "BP_destination"

		vl_Destination = Pend_For_New_Task@("ITMA_TPM_Choix_Destination",
							vm_PMV.NumEqt,485,130)
		vm_Destination[0] = vl_Destination[1]
		vm_Lect_PMV.Message2      = ""
		vm_Lect_PMV.Destination   = vl_Destination[0] + 0
		ITMA_TPM_Demande_Calcul_TDP ( vm_Lect_PMV.NumEqt,
					vm_Lect_PMV.Destination,
					vm_Lect_PMV.AltDestination,
					vm_Lect_PMV.DestinationNoeud,
					vm_Lect_PMV.SiteGestion)
		ITMA_TPM_MAJ_PMV("nouveau")
		
		
		/*mise en commentaire hilmarcher************
		ITMA_TPM_Tabuler_ligne(vl_Destination[1],vl_tptext,vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Message2 = UPPERCASE@(vl_tptext)
		vm_Lect_PMV.Destination	= vl_Destination[0] + 0
		ITMA_TPM_Demande_Calcul_TDP ( vm_Lect_PMV.NumEqt, 
				vm_Lect_PMV.Destination, vm_Lect_PMV.AltDestination, vm_Lect_PMV.DestinationNoeud )
		ITMA_TPM_Tabuler_ligne(CM_AFF_TEXTE_TDP,vl_tptext,vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Message3	= vl_tptext
		ITMA_TPM_MAJ_PMV ( "nouveau" )
		*****************************************/
		
 	/*B appui sur le bouton destination alternat */
	CASE "BP_destination_alt"

		vl_Destination = Pend_For_New_Task@("ITMA_TPM_Choix_Destination",
							vm_PMV.NumEqt,485,130)
		ITMA_TPM_Tabuler_ligne(vl_Destination[1],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Alternat2		= UPPERCASE@(vl_tptext)
		vm_Lect_PMV.AltDestination	= vl_Destination[0] + 0
		ITMA_TPM_Demande_Calcul_TDP ( vm_Lect_PMV.NumEqt, 
				vm_Lect_PMV.Destination, vm_Lect_PMV.AltDestination, vm_Lect_PMV.DestinationNoeud )
		ITMA_TPM_Tabuler_ligne(CM_AFF_TEXTE_TDP,vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Alternat3		= vl_tptext
		ITMA_TPM_MAJ_PMV ( "nouveau" )
		
	/*B selection d'une proposition*/	
  	CASE "TA_proposition"

		DB_CTRL_GRAYED@(vm_fenetre,"BP_message",False)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_alternat",False)

	/*B selection d'un message*/	
  	CASE "BP_message"

	
		vl_lecture = DB_TABLE_GET_SELECTIONS@(vm_fenetre,"TA_proposition")
		vl_rang = vl_lecture[0]
		vl_lecture= DB_TABLE_GET_DATA@(vm_fenetre,"TA_proposition")
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,1],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Message1	= vl_tptext
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,2],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Message2	= vl_tptext
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,3],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Message3	= vl_tptext
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,0],vl_tptext,6)
		vm_Lect_PMV.TextePicto = vl_tptext

		if DB_CTRL_GET_VALUE@(vm_fenetre,"BA_cligno_picto")=True
		{	vm_Lect_PMV.ClignotementPicto = XDC_PICTO_CLIGNO }
		else 
		{	vm_Lect_PMV.ClignotementPicto = XDC_PICTO_PASCLIGNO }

		if DB_CTRL_GET_VALUE@(vm_fenetre,"BA_cligno1")=True
		{	vm_Lect_PMV.Clignotement1 = XDC_PMV_CLIGNOLENT }
		else 
		{	vm_Lect_PMV.Clignotement1 = XDC_PMV_PASCLIGNO }

		if DB_CTRL_GET_VALUE@(vm_fenetre,"BA_cligno2")=True and 
			vm_Config_PMV[vm_PMV.NumEqt].NbLigne >= 2
		{	vm_Lect_PMV.Clignotement2 = XDC_PMV_CLIGNOLENT }
		else 
		{	vm_Lect_PMV.Clignotement2 = XDC_PMV_PASCLIGNO }

		if DB_CTRL_GET_VALUE@(vm_fenetre,"BA_cligno3")=True and 
				vm_Config_PMV[vm_PMV.NumEqt].NbLigne >=3
		{	vm_Lect_PMV.Clignotement3 = XDC_PMV_CLIGNOLENT }
		else 
		{	vm_Lect_PMV.Clignotement3 = XDC_PMV_PASCLIGNO }

		for i = 0 to ARRAY_SIZE@(vm_picto)-1
			if trim@(vm_pix_table_proposition[vl_rang,0]) = trim@(vm_picto[i,0])
			{ 
				vm_Lect_PMV.TypePicto = vm_picto[i,2] 
			}
		next i

		vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE
		ITMA_TPM_MAJ_PMV ( "nouveau" )

		DB_CTRL_GRAYED@(vm_fenetre,"BP_message",True)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_alternat",True)

		vl_tptext = "Status : MAJ du message & du picto du PMV "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		DB_TABLE_SET_SELECTIONS@(vm_fenetre,"TA_proposition",NULL)


	/*B selection d'un alternat*/	
  	CASE "BP_alternat"

		vl_lecture = DB_TABLE_GET_SELECTIONS@(vm_fenetre,"TA_proposition")
		vl_rang = vl_lecture[0]
		vl_lecture= DB_TABLE_GET_DATA@(vm_fenetre,"TA_proposition")
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,1],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Alternat1	= vl_tptext
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,2],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Alternat2	= vl_tptext
		ITMA_TPM_Tabuler_ligne(vl_lecture[vl_rang,3],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
		vm_Lect_PMV.Alternat3	= vl_tptext

		if vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP
		{	vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE }
		vm_Lect_PMV.AltDestination = 0
		
		ITMA_TPM_MAJ_PMV ( "nouveau" )

		DB_CTRL_GRAYED@(vm_fenetre,"BP_alternat",True)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_message",True)

		vl_tptext = "Status : MAJ de l'alternat du PMV "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
 
		DB_TABLE_SET_SELECTIONS@(vm_fenetre,"TA_proposition",NULL)


	/*A SI action sur bouton copier / coller   */
	CASE "BP_copier_coller"
		vm_Lect_PMV = vm_PMV
		ITMA_TPM_MAJ_PMV ( "nouveau" )

		vl_tptext = "Status : Copie de l'état actuel du PMV"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)


	/*B selection d'un element dans la liste des fiches a traiter*/	
  	CASE "TA_Evenements"
		vl_tptext = "Status : selection evenement"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		tl_retour=ITMA_COM_Validite_Cause("ITMA_TPM_pilotage_d_un_PMV",
										vm_fenetre,tl_liste_causes)
		vm_Lect_PMV.Numero_Evt = tl_retour[0][0]
		vm_Lect_PMV.Cle_Evt = tl_retour[0][1]
		vl_fmc = tl_retour[1]
		if (vm_Lect_PMV.Numero_Evt<>0 and  vm_Lect_PMV.Cle_Evt<>0)
		{
			vm_Lect_PMV.fmc_liee = vl_fmc
		}
		else	vm_Lect_PMV.fmc_liee = "Pas de libellé F.M.C"
		vl_fmc1=SUBSTRING@(vl_fmc,1,46)
		vl_fmc2=TRIM@(SUBSTRING@(vl_fmc,46,20))

		DB_CTRL_DISPLAY@(vm_fenetre,"TA_Evenements",FALSE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",FALSE)

	      	DB_CTRL_TITLE@(vm_fenetre,"LI_fmc_liée_nouveau",vl_fmc1)
		DB_CTRL_TITLE@(vm_fenetre,"LI_date_fmc_nouveau",vl_fmc2)



	/*A SI action sur bouton executer  */
	CASE "BP_executer"

		tl_sel_pmv = DB_CTRL_GET_VALUE@(vm_fenetre,"BL_PMV")
		if NOT IS_ARRAY@ (tl_sel_pmv)				' mono-selection, simuler la multi-selection
			tl_sel_pmv = { tl_sel_pmv }

		if (ARRAY_SIZE@ (tl_sel_pmv) > 0)
		{
			if vm_validation_nouv_message = XDC_FAUX
			{
				vl_erreur = COM_OK
				for i = 0  to ARRAY_SIZE@ (tl_sel_pmv) - 1
					vl_rang = tl_sel_pmv[i]
					vl_numero = ARRAY_INDEX@(ARRAY_COLUMN@(vm_Config_PMV,1), vm_liste_PMV[vl_rang])
					vm_Lect_PMV.NumEqt = vl_numero
					vm_Lect_PMV.Identifiant = vm_liste_PMV[vl_rang]

					/* memoriser le numero du premier PMV, dont le retour terrain est attendu */
					if (i = 0) and (vm_PMV.NumEqt <> vl_numero) {
						vm_PMV.NumEqt = vl_numero
						ITMA_TPM_Reset_PMV ( "actuel" )
						ITMA_TPM_MAJ_PMV ( "actuel" )
						vm_PMV.TypeAffichage = vm_Lect_PMV.TypeAffichage
					}

					/* executer la commande sur le PMV */
					vl_coderet = ITMA_TPM_EXECUTER_PMV ()
					if vl_coderet <> COM_OK
						vl_erreur = COM_NOK
				next i
				if vl_erreur = COM_OK
				{
					DB_CTRL_TITLE@(vm_fenetre, "LI_fmc_liée_nouveau","Pas de fmc liée")
					DB_CTRL_TITLE@(vm_fenetre, "LI_date_fmc_nouveau","")
					vm_Lect_PMV.Numero_Evt	= 0
					vm_Lect_PMV.Cle_Evt	= 0
					vm_Lect_PMV.fmc_liee	= ""
				}
			}
			else 
			{
				info_message@("Execution impossible : Valider ou Annuler la modification du message...")	
			}
		}
		else 
		{
			info_message@("Sélectionner d'abord un ou plusieurs PMV ...")
		}


	/*A SI action sur bouton refuser le plan d'action pour le 1er PMV 
	CASE "BP_refuser"

		vl_tptext = "Status : Refus plan d'action"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",TRUE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_refuser",TRUE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_explication",TRUE)
		vl_retour[0] = COM_REFUSE*/


	/*A SI action sur liste  des pictos */
	CASE "TA_pictogrammes"

		vl_tptext = "Status : "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		if DB_EXIT_CODE@(vm_fenetre)= 50
 		{ 
			vl_tplecture 	= 	DB_TABLE_GET_SELECTIONS@(
								vm_fenetre,
								"TA_pictogrammes")

			DB_CTRL_TITLE@(vm_fenetre, "ID_picto_xxx_nouveau",
				vm_pix_table_picto_big[vl_tplecture[0],0])

			vm_Lect_PMV.TypePicto = vm_Liste_picto[vl_tplecture[0]] 

			if vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP
			{	vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE	}
		}
		

	/*A SI action sur liste  des PMV */
	CASE "BL_PMV"

		vl_tptext = "Status : "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		tl_sel_pmv = DB_CTRL_GET_VALUE@(vm_fenetre,"BL_PMV")
		IF NOT IS_ARRAY@ (tl_sel_pmv)				' mono-selection, simuler la multi-selection
			tl_sel_pmv = { tl_sel_pmv }

		vl_rang = tl_sel_pmv[ARRAY_SIZE@ (tl_sel_pmv) - 1]
		vl_numero = ARRAY_INDEX@(ARRAY_COLUMN@(vm_Config_PMV,1), vm_liste_PMV[vl_rang])

		if (vl_numero <> vm_PMV.NumEqt)
 		{ 
			vm_Lect_PMV.NumEqt = vm_etat_PMV[vl_numero].NumEqt
			vm_Lect_PMV.Identifiant = vm_liste_PMV[vl_rang]
			vm_Lect_PMV.SiteGestion = vm_Config_PMV[vl_numero].SiteGestion
			vm_PMV = vm_Lect_PMV

			vl_erreur = ITMA_TPM_Lecture_PMV()
			if vl_erreur<>COM_NOK then ITMA_TPM_MAJ_PMV  ( "actuel" )
			ITMA_TPM_MAJ_table_picto 

			IF (vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE)
			{
				ITMA_TPM_Reset_PMV ( "nouveau" )
				vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE
				ITMA_TPM_Lire_Neutre_Programme ()
				ITMA_TPM_MAJ_PMV ( "nouveau" )
			}

			if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP)
 			{ 
				vl_parametres[0].type = SYB#INT1_
				vl_parametres[0].data = vm_PMV.NumEqt
				vl_parametres[0].output = FALSE
				vl_parametres[1].type = SYB#INT1_
                        	vl_parametres[1].data = XDC_VRAI
                        	vl_parametres[1].output = FALSE
				IF COM05_SQL_Procedure (XZAO427_Lire_ZDP,
								vl_parametres,vl_resultats,C_MODULE) <> COM_OK
				{
					INFO_MESSAGE@("Erreur lors de la lecture des zones aval du PMV ")
					RETURN (COM_NOK)
				}
				vl_NumDesttdp = vl_resultats.select_results[0]
				vm_Lect_PMV.Destination = 0
				vm_Lect_PMV.AltDestination = 0
				vm_Lect_PMV.DestinationNoeud = 0

				/*le resultat contient au plus 2 dtp */
				if ( ((ARRAY_SIZE@(vl_NumDesttdp)>0) and 
						(ARRAY_SIZE@(vl_NumDesttdp)<4) and 
						(vm_Lect_PMV.TypeAffichage = CM_AFF_TDP)))
				{
					ITMA_TPM_Reset_PMV ( "nouveau" )
					vm_Lect_PMV.TypeAffichage = CM_AFF_TDP

					vl_NumDest = vl_NumDesttdp
					vm_Lect_PMV.Destination = vl_NumDest[0,0]
					vm_Lect_PMV.AltDestination = vl_NumDest[1,0]
					vm_Lect_PMV.DestinationNoeud = vl_NumDest[2,0]
					vm_Destination[0] = vl_NumDest[0,1]
					vm_Destination[1] = vl_NumDest[1,1]
					vm_Destination[2] = vl_NumDest[2,1]
					vm_Lect_PMV.Message2 = ""
					vm_Lect_PMV.TypePicto     = XDC_PICTO_PASPICTO
					ITMA_TPM_Demande_Calcul_TDP ( vm_Lect_PMV.NumEqt, 
							vm_Lect_PMV.Destination,
							vm_Lect_PMV.AltDestination,
							vm_Lect_PMV.DestinationNoeud,
							vm_Lect_PMV.SiteGestion)
					
					vm_Lect_PMV.Alternat1     = ""
					vm_Lect_PMV.Alternat2     = ""
					vm_Lect_PMV.Alternat3     = ""
					
					DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",FALSE)
				}
				else
				{
					info_message@("Pas d'affichage possible pour ce PMV ")
					ITMA_TPM_Reset_PMV ( "nouveau" )
					vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE
					DB_CTRL_VALUE@(vm_fenetre,"BO_Arret",vm_Lect_PMV.TypeAffichage)
					ITMA_TPM_MAJ_PMV ( "nouveau" )
				}	
			}
		}
				
		

	/*A SI action sur liste  des types de messages */
	CASE "BL_éléments"

		vl_tptext = "Status : "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
		vl_erreur = ITMA_TPM_BL_elements_click()	


	/*A SI action bouton mise au neutre du PMV */
	CASE "BO_Arret"

		vl_tptext = "Status : "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
		
		vm_Lect_PMV.TypeAffichage = DB_CTRL_GET_VALUE@(vm_fenetre,"BO_Arret")	

		if 	(vm_Lect_PMV.TypeAffichage > CM_AFF_TEXTE and 
			vm_Lect_PMV.TypeAffichage < CM_AFF_TDP)
		{
			ITMA_TPM_Reset_PMV ( "nouveau" )
			vm_Lect_PMV.TypeAffichage = DB_CTRL_GET_VALUE@(vm_fenetre,"BO_Arret")

			/* Si choix affichage du neutre, lire la mise au neutre programmee actuelle */
			IF (vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE) {
				ITMA_TPM_Lire_Neutre_Programme ()
			}

			ITMA_TPM_MAJ_PMV ( "nouveau" )
  		 	DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",FALSE)
		}

		/* Si choix neutre ou TDP */
		IF  (vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE) OR (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP)
		{
			/* si plusieurs PMV sont selectionnes alors ne garder que le dernier */
			/* (celui qui a fait l'objet de la derniere commande et dont l'etat est affiche) */
			IF (ARRAY_SIZE@ (tl_sel_pmv) > 1)
			{
				vl_rang = ARRAY_INDEX@(vm_liste_PMV,vm_PMV.Identifiant)
				DB_CTRL_MULTI_SELECT@(vm_fenetre,"BL_PMV",FALSE)
				IF (vl_rang >= 0)
				{
					tl_sel_pmv = { vl_rang }
					DB_CTRL_VALUE@(vm_fenetre, "BL_PMV", vl_rang)
				}
				else
				{
					tl_sel_pmv = { }
					DB_CTRL_VALUE@(vm_fenetre, "BL_PMV", -1)
				}
			}
		}

		/* Si choix aff TDP */
		if 	(vm_Lect_PMV.TypeAffichage = CM_AFF_TDP)
		{
			vl_parametres[0].type = SYB#INT1_
                        vl_parametres[0].data = vm_PMV.NumEqt
                        vl_parametres[0].output = FALSE
			vl_parametres[1].type = SYB#INT1_
                        vl_parametres[1].data = XDC_VRAI
                        vl_parametres[1].output = FALSE
			IF COM05_SQL_Procedure (XZAO427_Lire_ZDP,
					vl_parametres,vl_resultats,C_MODULE) <> COM_OK
			{
				INFO_MESSAGE@("Erreur lors de la lecture des zones aval du PMV ")
				RETURN (COM_NOK)
			}
			vl_NumDest = vl_resultats.select_results[0]
		
			/*le resultat contient au plus 2 dtp */
			if ( (ARRAY_SIZE@(vl_NumDest)>0) and (ARRAY_SIZE@(vl_NumDest)<4))
			{
				ITMA_TPM_Reset_PMV ( "nouveau" )
				vm_Lect_PMV.TypeAffichage = CM_AFF_TDP

				/* 3 dtp pour le PMV concerne */
/*				if (ARRAY_SIZE@(vl_NumDest) = 3)
				{
*/					vm_Lect_PMV.Destination = vl_NumDest[0,0]
					vm_Lect_PMV.AltDestination = vl_NumDest[1,0]
					vm_Lect_PMV.DestinationNoeud = vl_NumDest[2,0]
					vm_Destination[0] = vl_NumDest[0,1]
					vm_Destination[1] = vl_NumDest[1,1]
					vm_Destination[2] = vl_NumDest[2,1]
					vm_Lect_PMV.Message2 = ""
					vm_Lect_PMV.TypePicto     = XDC_PICTO_PASPICTO
					ITMA_TPM_Demande_Calcul_TDP ( vm_Lect_PMV.NumEqt,
									vm_Lect_PMV.Destination,
									vl_NumDest[1,0],
									vl_NumDest[2,0],
								vm_Lect_PMV.SiteGestion	)
/*				}
*/			}
			else
			{
				info_message@("Pas d'affichage Temps de parcours JM possible pour ce PMV ")
				ITMA_TPM_Reset_PMV ( "nouveau" )
				ITMA_TPM_MAJ_PMV ( "nouveau" )
			}
		}

	/*A SI action sur bascule de clignotement Picto ou PMV */
	CASE "BA_clignotement_picto"
	CASE "BA_clignotement1_nouveau"
	CASE "BA_clignotement2_nouveau"
	CASE "BA_clignotement3_nouveau"
		if  DB_CTRL_GET_VALUE@(vm_fenetre,"BA_clignotement_picto")
		{ vm_Lect_PMV.ClignotementPicto = XDC_PICTO_CLIGNO }
		else { vm_Lect_PMV.ClignotementPicto = XDC_PICTO_PASCLIGNO }

		if  DB_CTRL_GET_VALUE@(vm_fenetre,"BA_clignotement1_nouveau")
		{ vm_Lect_PMV.Clignotement1 = XDC_PMV_CLIGNOLENT }
		else { vm_Lect_PMV.Clignotement1 = XDC_PMV_PASCLIGNO }

		if  DB_CTRL_GET_VALUE@(vm_fenetre,"BA_clignotement2_nouveau")
		{ vm_Lect_PMV.Clignotement2 = XDC_PMV_CLIGNOLENT }
		else { vm_Lect_PMV.Clignotement2 = XDC_PMV_PASCLIGNO }

		if  DB_CTRL_GET_VALUE@(vm_fenetre,"BA_clignotement3_nouveau")
		{ vm_Lect_PMV.Clignotement3 = XDC_PMV_CLIGNOLENT }
		else { vm_Lect_PMV.Clignotement3 = XDC_PMV_PASCLIGNO }

		if vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP
		{	vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE }


  	/*A SI clic sur nouveau message  du  PMV */
	CASE "BL_direction_nouveau"
	CASE "BL_message_nouveau"
		if (vm_validation_nouv_message = XDC_FAUX and 
		   vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP) 

		{
			vl_tptext = "Status : MAJ message "
 			DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
			vm_Text_Actif = "message"
			ITMA_TPM_Appel_Modif_message("message")
		}

   	/*A SI clic sur nouvel alternat du  PMV */
	CASE "BL_alt_dir_nouveau"
	CASE "BL_alternat_nouveau"
		if (vm_validation_nouv_message = XDC_FAUX and 
		    vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP) 
		{
			vl_tptext = "Status : MAJ alternat "
 			DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
			vm_Text_Actif = "alternat"
			ITMA_TPM_Appel_Modif_message("alternat")
		}

  	/*A SI clic sur valider message  du  PMV */
 	CASE "BP_valider_nouveau"

		vl_tptext = "Status : Validation Modif."
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
		vm_validation_nouv_message = XDC_FAUX
		ITMA_TPM_Modif_message

  	/*A SI clic sur annuler modif message  du  PMV */
	CASE "BP_annuler_nouveau"

		vl_tptext = "Status : Annulation Modif. "
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		DB_CTRL_DISPLAY@(vm_fenetre,"BP_valider_nouveau",FALSE)	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_annuler_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif1",FALSE)	
		DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif2",FALSE)	
		DB_CTRL_DISPLAY@(vm_fenetre, "BS_modif3",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif_direction",FALSE)
		vm_validation_nouv_message = XDC_FAUX
		ITMA_TPM_MAJ_PMV ( "nouveau" )

		DB_CTRL_DISPLAY@(vm_fenetre,"BP_message",True)	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_alternat",True)
	
	
  	/*A SI clic sur bouton explication du  PMV */
 	/*CASE "BP_explication"

		vl_tptext = "Status : demande d'explication"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

		vl_erreur = PEND_FOR_NEW_TASK@("ITMA_TEX_Explication_Propo",
					 "ITMA_TPM_pilotage_d_un_PMV",  vm_PMV.Explication)*/


    	CASE "poke_"

     	CASE OF  DB_GET_POKE@ (vm_fenetre)

  		/*A SI reception message de FIN */
    		CASE COM_CANAL_FIN
    			vm_la_fenetre_est_active = FALSE

   		/*A SI reception etat PMV */
      	CASE COM_CANAL_MTPM_ETAT

			/*A capture du message */
			vl_message = DB_GET_POKE_DATA@(vm_fenetre)
			/*A décomposition du message reçu en tableau */
			vl_mess = ARRAY_FROM_STRING@(vl_message[0],",")
			vl_num_eqt = vl_mess[0]+0
			'COM01_Trace(COM_DEBUG2, "Recu etat PMV " ++ vl_num_eqt)

			/* indicateur etat recu = 1*/
			vm_etat_PMV[vl_num_eqt].NumeroAction		=1	

			ITMA_TPM_Tabuler_ligne(vl_mess[1],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Message1= vl_tptext 
			ITMA_TPM_Tabuler_ligne(vl_mess[5],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Message2= vl_tptext 
			ITMA_TPM_Tabuler_ligne(vl_mess[9],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Message3= vl_tptext 
		 
			ITMA_TPM_Tabuler_ligne(vl_mess[4],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Alternat1= vl_tptext 
			ITMA_TPM_Tabuler_ligne(vl_mess[8],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Alternat2= vl_tptext 
			ITMA_TPM_Tabuler_ligne(vl_mess[12],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
			vm_etat_PMV[vl_num_eqt].Alternat3= vl_tptext 
		 
			vm_etat_PMV[vl_num_eqt].clignotement1 = vl_mess[3] 
			vm_etat_PMV[vl_num_eqt].clignotement2 = vl_mess[7] 
			vm_etat_PMV[vl_num_eqt].clignotement3 = vl_mess[11] 

			if vm_Config_PMV[vl_num_eqt].NbLigne = CM_4LIGNES
			{
				ITMA_TPM_Tabuler_ligne(vl_mess[13],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
				vm_etat_PMV[vl_num_eqt].Direction= vl_tptext 
				ITMA_TPM_Tabuler_ligne(vl_mess[16],vl_tptext,vm_Config_PMV[vl_num_eqt].NbCaracteres)
				vm_etat_PMV[vl_num_eqt].AltDirection= vl_tptext 
				vm_etat_PMV[vl_num_eqt].Flash = vl_mess[17]
			}
			else
			{	vm_etat_PMV[vl_num_eqt].Flash = vl_mess[13] }

			if vm_PMV.NumEqt = vl_num_eqt 
			{ 	
				vm_PMV.Message1 		= vm_etat_PMV[vl_num_eqt].Message1
				vm_PMV.Message2 		= vm_etat_PMV[vl_num_eqt].Message2
				vm_PMV.Message3 		= vm_etat_PMV[vl_num_eqt].Message3
				vm_PMV.Alternat1 		= vm_etat_PMV[vl_num_eqt].Alternat1
				vm_PMV.Alternat2 		= vm_etat_PMV[vl_num_eqt].Alternat2
				vm_PMV.Alternat3 		= vm_etat_PMV[vl_num_eqt].Alternat3
				vm_PMV.Clignotement1	=
						 vm_etat_PMV[vl_num_eqt].Clignotement1	
	 			vm_PMV.Clignotement2 	= 
							vm_etat_PMV[vl_num_eqt].Clignotement2	
	 			vm_PMV.Clignotement3 	= 
							vm_etat_PMV[vl_num_eqt].Clignotement3	
				vm_PMV.Direction 		= vm_etat_PMV[vl_num_eqt].Direction
				vm_PMV.AltDirection 	= 
							vm_etat_PMV[vl_num_eqt].AltDirection
	 			vm_PMV.Flash			= vm_etat_PMV[vl_num_eqt].Flash

				ITMA_TPM_MAJ_PMV ( "actuel" )
			}
				
	
   		/*A SI reception etat Picto */
      	CASE COM_CANAL_MTPM_PICTO

			/*A capture du message */
			vl_message = DB_GET_POKE_DATA@(vm_fenetre)
			/*A décomposition du message reçu en tableau */
			vl_mess = ARRAY_FROM_STRING@(vl_message[0],",")
			vl_num_eqt = vl_mess[0]+0

			/*A MAJ text picto */
			ITMA_TPM_Tabuler_ligne(vl_mess[1],vl_tptext,6)
			vm_etat_PMV[vl_num_eqt].TextePicto = vl_tptext

			/*A MAJ picto */
			vm_etat_PMV[vl_num_eqt].TypePicto = 
							TRIM@(SUBSTRING@(vl_mess[2],1,4))
			/*A MAJ bascule cligno picto */
			vm_etat_PMV[vl_num_eqt].ClignotementPicto = vl_mess[3]

			if vm_PMV.NumEqt = vl_num_eqt 
			{ 	
				vm_PMV.TypePicto		= vm_etat_PMV[vl_num_eqt].TypePicto
	 			vm_PMV.TextePicto 		= vm_etat_PMV[vl_num_eqt].TextePicto
	  			vm_PMV.ClignotementPicto	=
							 vm_etat_PMV[vl_num_eqt].ClignotementPicto

				vl_tptext = "Status : Reception message Picto "
				DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

				ITMA_TPM_MAJ_PMV ( "actuel" )
			}

   		/*A SI reception calcul TDP */
      	CASE COM_CANAL_MTPM_TDP

			/*A capture du message */
			vl_message = DB_GET_POKE_DATA@(vm_fenetre)
			/*A décomposition du message reçu en tableau */
			vl_mess = null
			vl_mess = ARRAY_FROM_STRING@(vl_message[0],",")
			vl_num_eqt = vl_mess[0]+0
			vm_poke_tdp1=""
			vm_poke_tdp2=""
			vm_poke_tdp3=""
			if (vm_Lect_PMV.NumEqt = vl_num_eqt)
			{
				
				vl_tptext = " --> Recu TDP PMV : Dest1(" ++vm_Lect_PMV.Destination++")="++ vl_mess[1]++" Dest2("++vm_Lect_PMV.AltDestination++")="++vl_mess[2]++" Dest3("++vm_Lect_PMV.DestinationNoeud++")="++vl_mess[3]
				COM01_Trace(COM_DEBUG2,vl_tptext)
				if ((vl_mess[1] <> "") or (vl_mess[2] <> "") or (vl_mess[3] <> ""))
				{			
					vm_Lect_PMV.Message1 = ""
					vm_Lect_PMV.Message2 = ""
					vm_Lect_PMV.Message3 = ""

					vm_poke_tdp1 = vl_mess[1]
					if (vm_poke_tdp1 <> "")
					{
						ITMA_TPM_Tabuler_ligne_bis(vm_Destination[0],vm_poke_tdp1,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
						vm_Lect_PMV.Message1 = vl_tptext
					}
					vm_poke_tdp2 = vl_mess[2]
					if (vm_poke_tdp2 <> "")
					{
						ITMA_TPM_Tabuler_ligne_bis(vm_Destination[1],vm_poke_tdp2,vl_tptext,  vm_Config_PMV[vl_num_eqt].NbCaracteres)
						vm_Lect_PMV.Message2 = vl_tptext
					}
					vm_poke_tdp3 = vl_mess[3]
					if (vm_poke_tdp3 <> "")
					{
						ITMA_TPM_Tabuler_ligne_bis(vm_Destination[2],vm_poke_tdp3,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
						vm_Lect_PMV.Message3 = vl_tptext
					}
					
					if vm_validation_nouv_message = XDC_FAUX
					{
						ITMA_TPM_MAJ_PMV ( "nouveau" )
					}
				}
				else
				/* S'il n'y a aucune TDP valides, alors on passe en neutre */
				{
					vm_Lect_PMV.Message1 = ""
					vm_Lect_PMV.Message2 = ""
					vm_Lect_PMV.Message3 = ""
					vm_Lect_PMV.Destination = 0
 					vm_Lect_PMV.AltDestination = 0
					vm_Lect_PMV.DestinationNoeud = 0
					info_message@("Pas d'affichage Temps de parcours possible (zone HS)")
					ITMA_TPM_Reset_PMV ( "nouveau" )
					vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE
					DB_CTRL_VALUE@(vm_fenetre,"BO_Arret",vm_Lect_PMV.TypeAffichage)
					ITMA_TPM_MAJ_PMV ( "nouveau" )
				}
/*
				if ((vl_mess[1] <> "") and (vl_mess[2] <> ""))
				{
					vm_poke_tdp1 = vl_mess[1]
					vm_poke_tdp2 = vl_mess[2]
					ITMA_TPM_Tabuler_ligne_bis(vm_Destination[0],vm_poke_tdp1,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
					vm_Lect_PMV.Message1 = vl_tptext
					ITMA_TPM_Tabuler_ligne_bis(vm_Destination[1],vm_poke_tdp2,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
					vm_Lect_PMV.Message3 = vl_tptext
				}
				
				if ((vl_mess[1] <> "") and (vl_mess[2] = ""))
				{
 					vm_Lect_PMV.AltDestination = 0
					vm_poke_tdp1 = vl_mess[1]
					ITMA_TPM_Tabuler_ligne_bis(vm_Destination[0],vm_poke_tdp1,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
					vm_Lect_PMV.Message1 = vl_tptext
					vm_Lect_PMV.Message3 = ""
				}
				
				if ((vl_mess[1] = "") and (vl_mess[2] <> ""))
				{
					vm_Lect_PMV.Destination = 0
					vm_poke_tdp2 = vl_mess[2]
					ITMA_TPM_Tabuler_ligne_bis(vm_Destination[1],vm_poke_tdp2,vl_tptext, vm_Config_PMV[vl_num_eqt].NbCaracteres)
					vm_Lect_PMV.Message3 = vl_tptext
					vm_Lect_PMV.Message1 = ""
				}
*/				/* S'il n'y a aucune TDP valides, alors on passe en neutre */
/*				if  ((vl_mess[1] = "") and (vl_mess[2] = "") )
				{
					vm_Lect_PMV.Message1 = ""
					vm_Lect_PMV.Message3 = ""
					vm_Lect_PMV.Destination = 0
 					vm_Lect_PMV.AltDestination = 0
					info_message@("Pas d'affichage Temps de parcours possible (zone HS)")
					ITMA_TPM_Reset_PMV ( "nouveau" )
					vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE
					DB_CTRL_VALUE@(vm_fenetre,"BO_Arret",vm_Lect_PMV.TypeAffichage)
					ITMA_TPM_MAJ_PMV ( "nouveau" )
				}
				else
					if vm_validation_nouv_message = XDC_FAUX
					{
						ITMA_TPM_MAJ_PMV ( "nouveau" )
					}
*/			}

			

   		ENDCASE

	ENDCASE
	
	
	DB_CTRL_VALUE@(vm_fenetre,"BO_Arret",vm_Lect_PMV.TypeAffichage)
	if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP)
	{
		DB_CTRL_MULTI_SELECT@ (vm_fenetre, "BL_PMV", FALSE)
	}
	else
	{
		DB_CTRL_MULTI_SELECT@ (vm_fenetre, "BL_PMV", TRUE)
	}

/*PNI	if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP and 			
	   vm_validation_nouv_message = XDC_FAUX)
	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination",TRUE)
	}
	else
	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination",FALSE)
	}

	if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP and 			
	   vm_validation_nouv_message = XDC_FAUX and 
	   DB_CTRL_GET_DISPLAY@(vm_fenetre,"BL_alternat_nouveau") = TRUE)
	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination_alt",TRUE)
	}
	else
	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,"BP_destination_alt",FALSE)
	}*/
	
WEND

vl_tptext = "Stop ITMA_TPM_pilotage_d_un_PMV " ++ "   User : " ++ vm_NomOperateur
COM01_Trace(COM_INFO,vl_tptext)

RETURN(COM_QUITTE)

ENDMACRO

/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Executer
*  ----------------------------------------------------------------------------
*/
MACRO ITMA_TPM_EXECUTER_PMV
/*
* ARGUMENTS EN ENTREE :
*   aucun
*
*
* ARGUMENTS EN SORTIE : 
*
*
* CODE RETOUR         : auncun
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */
var 	vl_retour
var	vl_erreur
var	vl_index
var	vl_tptext

	vl_retour = NULL
	vl_erreur = COM_NOK 

	DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",TRUE)
/*	DB_CTRL_GRAYED@(vm_fenetre,"BP_refuser",TRUE)*/
	vm_Lect_PMV.TypeAffichage = DB_CTRL_GET_VALUE@(vm_fenetre,"BO_Arret")

	vl_tptext = FORMAT@ (" --> Exec PMV %s, Fmc %s/%s, dest:%s altdest:%s destnoeud:%s", vm_Lect_PMV.NumEqt,
	                     vm_Lect_PMV.Numero_Evt, vm_Lect_PMV.Cle_Evt, vm_Lect_PMV.Destination, vm_Lect_PMV.AltDestination, vm_Lect_PMV.DestinationNoeud)
	COM01_Trace(COM_INFO,vl_tptext)
	vl_tptext = " --> Texte=" ++ vm_Lect_PMV.Message1 ++ "/" ++ vm_Lect_PMV.Message2 ++ "/"++vm_Lect_PMV.Message3
	COM01_Trace(COM_INFO,vl_tptext)
	
	if (SYSTEM_VAR@(vg_site) = XDC_CI and NOT vm_mode_formation)
	{
		if NOT ITMA_COM_District_pilotable(vm_Config_PMV[vm_PMV.NumEqt].sitegestion, 
			XDC_LIB_PMV,TRUE,vm_Config_PMV[vm_PMV.NumEqt].Identifiant)
		{ 
			RETURN(COM_NOK)
 		}
 	}

	if (vm_Lect_PMV.Numero_Evt<>NULL and vm_Lect_PMV.Cle_Evt<>NULL) or
	   (vm_Lect_PMV.TypeAffichage > CM_AFF_TEXTE and 
	    vm_Lect_PMV.TypeAffichage < CM_AFF_TDP )	or 
		( vm_Lect_PMV.TypeAffichage = CM_AFF_TDP and vm_Lect_PMV.TypePicto= XDC_PICTO_PASPICTO)
	{
		vl_retour = COM_VALIDE
		if vm_Lect_PMV.TypeAffichage <= CM_AFF_TEXTE or 
		   vm_Lect_PMV.TypeAffichage = CM_AFF_TDP
		{
			vm_Lect_PMV.TextePicto = DB_CTRL_GET_VALUE@( vm_fenetre, "BS_nota_picto")	
			vm_Lect_PMV.TextePicto = vm_Lect_PMV.TextePicto ++ "      "
			vm_Lect_PMV.TextePicto = SUBSTRING@(vm_Lect_PMV.TextePicto,1,6)
			/* Test type picto */
			vl_index = ARRAY_INDEX@(vm_liste_picto,vm_Lect_PMV.TypePicto )
			if vl_index = -1 AND vm_Config_PMV[vm_Lect_PMV.NumEqt].picto=1
			{ 
				info_message@("PMV  : Picto interdit")
				vl_erreur = COM_NOK 
			}
			else
			{
				if NOT vm_mode_formation
				{ vl_erreur = ITMA_TPM_Commande_PMV() }
				if vl_erreur = COM_OK
				{
					vm_PMV.Numero_Evt	= vm_Lect_PMV.Numero_Evt
 					vm_PMV.Cle_Evt		= vm_Lect_PMV.Cle_Evt
/* 					if (vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP )vm_PMV.fmc_liee	= vm_Lect_PMV.fmc_liee*/
					ITMA_TPM_MAJ_PMV ( "actuel" )
				}
			}
		}
		else
		{
			if 	(vm_Lect_PMV.TypeAffichage = CM_AFF_TEMPER) 
			{
				INFO_MESSAGE@("Affichage température interdit")
				COM01_Trace(COM_WARNING,"Affichage température refusé")
				RETURN(COM_NOK)
			}

			if 	(vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE) or
			   	(vm_Lect_PMV.TypeAffichage = CM_AFF_HEURE) or
				(vm_Lect_PMV.TypeAffichage = CM_AFF_TEMPER) or
				(vm_Lect_PMV.TypeAffichage = CM_AFF_ETEINT) 
			{
				if NOT vm_mode_formation
				{ vl_erreur = ITMA_TPM_Commande_Fin_PMV() }
			}
			else
			{	vl_erreur = COM_NOK }
		}
			
		if vl_erreur = COM_NOK
		{	
			info_message@("Commande PMV numéro " ++ vm_Lect_PMV.NumEqt ++ " impossible")
		}
	}
	else
	{
		info_message@("Vous devez lier cette action à une FMC")
		DB_CTRL_GRAYED@(vm_fenetre,"BP_lier_fmc",FALSE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_executer",FALSE)
		vl_erreur = COM_NOK
	} 

	RETURN(vl_erreur)

ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	MAJ de la liste des pictogrammes
*
*  ----------------------------------------------------------------------------
*/
MACRO ITMA_TPM_MAJ_table_picto
/*
* ARGUMENTS EN ENTREE :
*   	aucun
*
*
* ARGUMENTS EN SORTIE : 
*
*
* CODE RETOUR         : auncun
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */

	var	i			' index de loop1
	var 	j			' index de loop2
	var 	k			' index de loop3
	var	vl_trouve		' flag de condition
	var	vl_taille		' taille d'une table
	var	vl_Fichier	' nom du fichier associé au type de picto
	var	vl_liste_picto_tpm	' liste tampon
	var	vl_tptext			' zone de text tampon


	IF vm_Config_PMV[vm_Lect_PMV.NumEqt].picto<>1
		RETURN ( COM_OK)

	vl_tptext = "--> ITMA_TPM_MAJ_table_picto pour Eqt= " ++ vm_Lect_PMV.NumEqt
	COM01_Trace(0,vl_tptext)

	vm_table_picto 	= ""
	vm_table_picto[0,0] 	= ""
	vm_titre_table_picto[0][0] 	= "Libellé"
	vm_titre_table_picto[0][1]	= 150
	vm_pix_table_picto	= ""
	vm_pix_table_picto[0]	= ""
	vm_pix_table_picto_big	= ""
	vm_pix_table_picto_big[0]	= ""
	vm_liste_picto = ""
	vm_liste_picto[0]= ""
	
	vl_Fichier = REP_CONFIG ++ "pmv/" ++ vm_type_Picto[vm_PMV.NumEqt]

	/*A si le fichier de config du type picto existe */
	if FILE_EXISTS@(vl_Fichier)
	{
		/*A lecture du fichier de config */
		vl_liste_picto_tpm = READ_ASCII_FILE@(vl_Fichier)
		vl_taille = array_size@(vl_liste_picto_tpm)

		/*A MAJ de la table liste_picto via le fichier */
		for i = 0 to vl_taille-1
			vm_liste_picto[i] =
			UPPERCASE@(TRIM@(vl_liste_picto_tpm[i]))
		next i
	}
	else
	{
		DB_TABLE_SET_DATA@(vm_fenetre,
			"TA_pictogrammes",
			vm_table_picto,
			vm_titre_table_picto,
			vm_pix_table_picto)
		info_message@("Pas de type de PMV détecté...")
		RETURN(COM_NOK)
	}	

	i = 0
	j = 0

	WHILE (vm_liste_picto[i]<>NULL) or
	  (TRIM@(vm_liste_picto[i])<>"") or (i<25)
		vl_trouve = -1
		for k = 0 to 30
			 if TRIM@(vm_liste_picto[i])=TRIM@(vm_picto[k,2])
			{ vl_trouve = k }
		next k	
		k = k + 1
		if vl_trouve > -1 and vl_trouve < 30
		{
			vm_table_picto[j,0]		= vm_picto[vl_trouve,3]
			vm_pix_table_picto[j,0]	= vm_picto[vl_trouve,0]
			vm_pix_table_picto[j,1]	= ""
			vm_pix_table_picto_big[j,0]	= vm_picto[vl_trouve,1]
			j = j +1
		}
		i = i + 1
	WEND	

	DB_TABLE_SET_NEW_TOP_ROW@(vm_fenetre,"TA_pictogrammes",0)
	DB_TABLE_SET_MARKER_WIDTH@(vm_fenetre,"TA_pictogrammes",24)	DB_TABLE_SET_DATA@(vm_fenetre,"TA_pictogrammes",vm_table_picto,vm_titre_table_picto,vm_pix_table_picto)

	DB_CTRL_DISPLAY@(vm_fenetre,"Liste_picto",TRUE)
	DB_CTRL_DISPLAY@(vm_fenetre,"TA_pictogrammes",TRUE)

ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Capture le click de la liste des éléments
*				MAJ des lignes 1,2 et 3
*
* -----------------------------------------------------------------------------
*/
MACRO ITMA_TPM_BL_elements_click
/*
* ARGUMENTS EN ENTREE :
*
*
* ARGUMENTS EN SORTIE : 
*
*
* CODE RETOUR         : COM_OK ou COM_NOK
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */
	
	var	vl_Lect_elements ' ligne séléctioné dans la liste des éléments
	var	vl_pos		' tampon de position de la liste
	var	i,j			' index de loop
	var	vl_ele_table	' tableau de lecture des elements
	var	vl_ele_propo	' tableau de lecture des elements
	var	vl_Fichier	' nom du fichier de proposition
	var	vl_tpm
	var	vl_tpm2
	var	vl_picto
	var	vl_tptext		' zone de text tampon

/*A trace de l'appel de procedure */
vl_tptext =  "--> ITMA_TPM_BL_elements_click"
COM01_Trace(0,vl_tptext)

vl_pos =	DB_CTRL_GET_VALUE@(vm_fenetre,"BL_éléments")
	
if vl_pos<0 { vl_pos = 0 }

vl_Lect_elements =	TRIM@(TABS_TO_SPACES@(vm_liste_elements[vl_pos]))
	
vl_Fichier = REP_CONFIG ++ "pmv/Config_Element_" ++ vl_Lect_elements ++ ".cfg" 

/*A Si fichier de config existant */
if FILE_EXISTS@(vl_Fichier)
{
	vl_tpm2 = READ_ASCII_FILE@(vl_Fichier)
	for i = 0 to ARRAY_SIZE@(vl_tpm2)-1
		vl_tpm = array_from_string@(vl_tpm2[i],",")
		for j = 0 to array_size@(vl_tpm)-1
			vl_ele_propo[i,j] = TRIM@(TABS_TO_SPACES@(vl_tpm[j]))
		next j
	next i
	
	vm_pix_table_proposition	= ""
	vm_table_proposition		= ""
	for i = 0 to ARRAY_SIZE@(vl_ele_propo)-1
		for j = 0 to ARRAY_SIZE@(vm_picto)-1
			if trim@(vl_ele_propo[i,0]) = trim@(vm_picto[j,2])
			{ 	vl_picto=vm_picto[j,0] }
		next j
		vm_pix_table_proposition[i]	= vl_picto,""
		vm_table_proposition[i,0]	= vl_ele_propo[i,1]
		vm_table_proposition[i,1]	= vl_ele_propo[i,2]
		vm_table_proposition[i,2]	= vl_ele_propo[i,3]
		vm_table_proposition[i,3]	= vl_ele_propo[i,4]
	next i

	DB_TABLE_SET_NEW_TOP_ROW@(vm_fenetre,"TA_proposition",0)
	DB_TABLE_SET_MARKER_WIDTH@(vm_fenetre,"TA_proposition",24)
	DB_TABLE_SET_DATA@( vm_fenetre,
					"TA_proposition",
					vm_table_proposition,
					vm_titre_table_proposition)
	DB_TABLE_MARKER_PIXMAPS@(vm_fenetre,
						"TA_proposition",
						0,
						vm_pix_table_proposition)  		

}


RETURN(COM_OK)
ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Centre un message sur une zone de 'n' caractère
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Tabuler_ligne(va_old,va_new,va_taille)

/*
* ARGUMENTS EN ENTREE :
*   	va_old        	: Chaine en entrée ;
*	va_taille		: taille de la chaine de sortie
*
*
* ARGUMENTS EN SORTIE : 
*   va_new     		: Chaine en sortie.
*
*
* CODE RETOUR         : auncun
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */

	var	vl_long		' longeur du message
	var 	x			' variable static
	var 	i			' index de loop
	var		vl_tptext					' zone de text tampon

	vl_long = (len@(trim@(va_old)))
	x = int@((va_taille-vl_long)/2)
	va_new = ""
	for i = 1 to x
		va_new = va_new ++ " "
	next i
	vl_tptext = va_new ++ trim@(va_old) ++ "               "
	va_new = SUBSTRING@(vl_tptext,1,va_taille)
ENDMACRO



/*Hilmarcher */
/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :       Centre un message sur une zone de 'n' caractère
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Tabuler_ligne_bis(va_dest,va_tdp,va_new,va_nbcar)

/*
* ARGUMENTS EN ENTREE :
*       va_dest          : Chaine destination en entrée ;
*       va_tdp           : Chaine tdp en entrée ;
*
*
* ARGUMENTS EN SORTIE :
*   va_new              : Chaine en sortie.
*
*
* CODE RETOUR         : auncun
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */

	var     vl_long1,vl_long2       ' longueur du message destination et tdp
	var     x                       ' variable static
	var     i                       ' index de loop
	var     vl_space                ' longueur de l espace entre dest et tdp
	var     vl_tptext               ' zone de text tampon

	vl_long1 = (len@(trim@(va_dest)))
	vl_long2 = (len@(trim@(va_tdp)))
	x = int@((va_nbcar-(vl_long1 + vl_long2)))
	vl_space = ""
	for i = 1 to x
		vl_space = vl_space ++ " "
	next i
	vl_tptext = trim@(va_dest) ++ vl_space ++ trim@(va_tdp)
	va_new = SUBSTRING@(vl_tptext,1,va_nbcar)
ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Modifications du message et MAJ de la BL
*
*  ----------------------------------------------------------------------------
*/
MACRO ITMA_TPM_Modif_message
/*
* ARGUMENTS EN ENTREE :
*
*
* ARGUMENTS EN SORTIE : 
*
*
* CODE RETOUR         : COM_OK ou COM_NOK
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */
	
var	vl_objet		' object en cours de modification
var	vl_tplecture	' text tampon de lecture d'une liste
var	vl_tptext		' zone de text tampon


	vl_tplecture[0] = DB_CTRL_GET_VALUE@(vm_fenetre,"BS_modif1")
	vl_tplecture[0] = SUBSTRING@(TRIM@(vl_tplecture[0]),1,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne(vl_tplecture[0],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	vl_tplecture[0] = vl_tptext
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif1",FALSE)
	
	vl_tplecture[1] = DB_CTRL_GET_VALUE@(vm_fenetre,"BS_modif2")
	vl_tplecture[1] = SUBSTRING@(TRIM@(vl_tplecture[1]),1,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne(vl_tplecture[1],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	vl_tplecture[1] = vl_tptext
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif2",FALSE)
	
	vl_tplecture[2] = DB_CTRL_GET_VALUE@(vm_fenetre,"BS_modif3")
	vl_tplecture[2] = SUBSTRING@(TRIM@(vl_tplecture[2]),1,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne(vl_tplecture[2],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	vl_tplecture[2] = vl_tptext
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif3",FALSE)
	
	vl_objet = "BL_"++ vm_Text_Actif ++ "_nouveau"
	DB_CTRL_STRINGS@(vm_fenetre,vl_objet,vl_tplecture)
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,-1)

	vl_tplecture[3] = DB_CTRL_GET_VALUE@(vm_fenetre,"BS_modif_direction")
	vl_tplecture[3] = SUBSTRING@(TRIM@(vl_tplecture[3]),1,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne(vl_tplecture[3],vl_tptext,vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres)
	vl_tplecture[3] = vl_tptext
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif_direction",FALSE)

	if (vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres=15)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,9)
	else if (vm_Config_PMV[vm_PMV.NumEqt].NbCaracteres=18)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,12)

	if (vm_Text_Actif = "message")
	{
		vm_Lect_PMV.Message1 	= vl_tplecture[0]
  		vm_Lect_PMV.Message2 	= vl_tplecture[1]
		vm_Lect_PMV.Message3 	= vl_tplecture[2]
		vm_Lect_PMV.Direction 	= vl_tplecture[3]
	}
	else
	{
		vm_Lect_PMV.Alternat1 	= vl_tplecture[0]
		vm_Lect_PMV.Alternat2 	= vl_tplecture[1]
		vm_Lect_PMV.Alternat3 	= vl_tplecture[2]
		vm_Lect_PMV.AltDirection	= vl_tplecture[3]
	}

	
	DB_CTRL_DISPLAY@(vm_fenetre,"BP_valider_nouveau",FALSE)
	DB_CTRL_DISPLAY@(vm_fenetre,"BP_annuler_nouveau",FALSE)

	/*A Afficher les boutons de MAJ de message semi-automatique */
	DB_CTRL_DISPLAY@(vm_fenetre,"BP_message",True)	
	DB_CTRL_DISPLAY@(vm_fenetre,"BP_alternat",True)	


	/*A Afficher le texte saisi */
	vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE
	ITMA_TPM_MAJ_PMV ( "nouveau" )

	RETURN(COM_OK)

ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Affichage d'un BE pour modification du message
*
*  ----------------------------------------------------------------------------
*/
MACRO ITMA_TPM_Appel_Modif_message ( va_Type )
/*
* ARGUMENTS EN ENTREE :
*	va_Type : type d'affichage (message ou alternat)
*
*
* ARGUMENTS EN SORTIE : 
*
*
* CODE RETOUR         : COM_OK ou COM_NOK
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
---------------------------------------------------------------------------- */

var	vl_tplecture		' text tampon de lecture d'une liste
	

DB_CTRL_LENGTH@(vm_fenetre,"BS_modif1",vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
DB_CTRL_LENGTH@(vm_fenetre,"BS_modif2", vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
DB_CTRL_LENGTH@(vm_fenetre,"BS_modif3", vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
DB_CTRL_LENGTH@(vm_fenetre,"BS_modif_direction", vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
vl_tplecture = DB_CTRL_GET_STRINGS@(vm_fenetre,"BL_"++ va_Type ++ "_nouveau")
vm_Lect_PMV.TextePicto = DB_CTRL_GET_VALUE@( vm_fenetre, "BS_nota_picto")	
vm_Lect_PMV.TextePicto = vm_Lect_PMV.TextePicto ++ "      "
vm_Lect_PMV.TextePicto = SUBSTRING@(vm_Lect_PMV.TextePicto,1,6)

DB_CTRL_DISPLAY@(vm_fenetre,"BL_message_nouveau",FALSE)
DB_CTRL_DISPLAY@(vm_fenetre,"BL_alternat_nouveau",FALSE)
DB_CTRL_DISPLAY@(vm_fenetre,"BL_direction_nouveau",FALSE)
DB_CTRL_DISPLAY@(vm_fenetre,"BL_alt_dir_nouveau",FALSE)

DB_CTRL_VALUE@(vm_fenetre,"BS_modif1",TRIM@(vl_tplecture[0]))
DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif1",TRUE)	

DB_CTRL_VALUE@(vm_fenetre,"BS_modif2",TRIM@(vl_tplecture[1]))
DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif2",TRUE)	
if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP) and
   (va_Type = "message" or vm_Lect_PMV.AltDestination > 0 )
{	DB_CTRL_GRAYED@(vm_fenetre,"BS_modif2",TRUE) }
else
{	DB_CTRL_GRAYED@(vm_fenetre,"BS_modif2",FALSE) }

DB_CTRL_VALUE@(vm_fenetre,"BS_modif3",TRIM@(vl_tplecture[2]))
DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif3",TRUE)	
if (vm_Lect_PMV.TypeAffichage = CM_AFF_TDP) and
   (va_Type = "message" or vm_Lect_PMV.AltDestination > 0 )
{	DB_CTRL_GRAYED@(vm_fenetre,"BS_modif3",TRUE) }
else
{	DB_CTRL_GRAYED@(vm_fenetre,"BS_modif3",FALSE) }

if vm_Config_PMV[vm_PMV.NumEqt].NbLigne = CM_4LIGNES
{
	if va_Type = "message"
	{	vl_tplecture = DB_CTRL_GET_STRINGS@(vm_fenetre,"BL_direction_nouveau") }
	else
	{	vl_tplecture = DB_CTRL_GET_STRINGS@(vm_fenetre,"BL_alt_dir_nouveau") }
	DB_CTRL_VALUE@(vm_fenetre,"BS_modif_direction",TRIM@(vl_tplecture[0]))
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif_direction",TRUE)	
}

DB_CTRL_DISPLAY@(vm_fenetre,"BP_valider_nouveau",TRUE)	
DB_CTRL_DISPLAY@(vm_fenetre,"BP_annuler_nouveau",TRUE)

/*A griser les boutons de MAJ de message semi-automatique */
DB_CTRL_DISPLAY@(vm_fenetre,"BP_message",False)	
DB_CTRL_DISPLAY@(vm_fenetre,"BP_alternat",False)	

if (vm_Config_PMV[vm_Lect_PMV.NumEqt].NbLigne>2)
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif3",TRUE)
else 
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif3",FALSE)

if (vm_Config_PMV[vm_Lect_PMV.NumEqt].NbLigne>3)
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif_direction",TRUE)
else
	DB_CTRL_DISPLAY@(vm_fenetre,"BS_modif_direction",FALSE)
/*A mise a jour du flag de modification d'un message */
vm_validation_nouv_message = XDC_VRAI
DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_PMV",FALSE)
DB_CTRL_GRAYED@(vm_fenetre,"BP_lier_fmc",TRUE)
DB_CTRL_GRAYED@(vm_fenetre,"BO_Arret",TRUE)
DB_CTRL_GRAYED@(vm_fenetre,"BS_nota_picto",TRUE)
DB_CTRL_GRAYED@(vm_fenetre,"BP_copier_coller",TRUE)

RETURN(COM_OK)
					
ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Lecture des PMV disponibles
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Lecture_PMV

/*
* ARGUMENTS EN ENTREE 	:
*   aucun
*
*
* ARGUMENTS EN SORTIE	: 
*	va_liste_PMV	: Liste des PMV disponibles
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*   MAJ de la liste des PMV disponibles dans la fiche de pilotage des PMV.
*
---------------------------------------------------------------------------- */


	VAR	vl_etat_null		' flag d'etat_PMV NULL
	VAR	i				' index de boucle
	var	vl_tptext			' zone de text tampon
	VAR	vl_erreur

	/*A trace de l'appel de procedure */
	vl_tptext =  "--> XZAT05_Utilisation_PMV "++ vm_PMV.NumEqt
	COM01_Trace(0,vl_tptext)

	/*A si n° Eqt du PMV nul : sortir */
	if ( vm_PMV.NumEqt=NULL or TRIM@(vm_PMV.NumEqt)="" )
	{
		vl_tptext = "ITMA_TPM_Lecture_PMV refusee:"++vm_PMV.NumEqt
		COM01_Trace(COM_WARNING,vl_tptext)
		RETURN(COM_NOK)
	}


	if	vm_etat_PMV[vm_PMV.NumEqt+0].NumeroAction > 0	
	{ vl_etat_null = False }
	else
	{ vl_etat_null = True }

	vl_erreur = ITMA_TPM_Lecture_Etat_PMV ( vm_mode_formation, vm_PMV,
									 vl_etat_null, C_MODULE )


	/*A si pas etat_PMV null : MAJ avec l'etat terrain  */
	if ( NOT vl_etat_null ) 
	{
	 	vm_PMV.Message1 		= vm_etat_PMV[vm_PMV.NumEqt+0].Message1
  		vm_PMV.Message2 		= vm_etat_PMV[vm_PMV.NumEqt+0].Message2
	 	vm_PMV.Message3 		= vm_etat_PMV[vm_PMV.NumEqt+0].Message3
	 	vm_PMV.Alternat1 		= vm_etat_PMV[vm_PMV.NumEqt+0].Alternat1
	 	vm_PMV.Alternat2 		= vm_etat_PMV[vm_PMV.NumEqt+0].Alternat2
	 	vm_PMV.Alternat3 		= vm_etat_PMV[vm_PMV.NumEqt+0].Alternat3
		vm_PMV.TypePicto		= vm_etat_PMV[vm_PMV.NumEqt+0].TypePicto
	 	vm_PMV.TextePicto 		= vm_etat_PMV[vm_PMV.NumEqt+0].TextePicto
	 	vm_PMV.Direction		= vm_etat_PMV[vm_PMV.NumEqt+0].Direction
	 	vm_PMV.AltDirection 	= vm_etat_PMV[vm_PMV.NumEqt+0].AltDirection
	 	vm_PMV.Clignotement1	= vm_etat_PMV[vm_PMV.NumEqt+0].Clignotement1	
	 	vm_PMV.Clignotement2	= vm_etat_PMV[vm_PMV.NumEqt+0].Clignotement2	
	 	vm_PMV.Clignotement3	= vm_etat_PMV[vm_PMV.NumEqt+0].Clignotement3	
	 	vm_PMV.Flash			= vm_etat_PMV[vm_PMV.NumEqt+0].Flash
	  	vm_PMV.ClignotementPicto	= vm_etat_PMV[vm_PMV.NumEqt+0].ClignotementPicto
	}

RETURN (vl_erreur)

ENDMACRO


/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	MAJ de l'IHM avec la table PMV
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_MAJ_PMV ( va_type )

/*
* ARGUMENTS EN ENTREE : 
*	va_type : actuel ou nouveau
*
*
* ARGUMENTS EN SORTIE : aucun
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*   MAJ de l'IHM par rapport à la table PMV.
*
---------------------------------------------------------------------------- */

	var		vl_objet		' l'objet en cours de modif.
	var		i			' index de loop
	var		vl_taille		' taille du text
	var		vl_fmc1		' libellé fmc 1
	var		vl_fmc2		' libellé fmc 2
	var		vl_tplecture	' text tampon de lecture d'une liste
	var		vl_tptext		' zone de text tampon
	var		vl_tpaffichage	' flag de la bascule d'affichage
	var format type_PMV_GEN vl_PMV


	/*A trace de l'appel de procedure */
	vl_tptext =  "--> ITMA_TPM_MAJ_PMV" ++ va_type
	COM01_Trace(0,vl_tptext)

	if ( vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP )
	{ 
		vm_Lect_PMV.Destination 	= 0
 		vm_Lect_PMV.AltDestination = 0
		vm_Lect_PMV.DestinationNoeud = 0
	}

	vl_tpaffichage = CM_AFF_NEUTRE

	if ( va_type = "nouveau" )
	{ vl_PMV = vm_Lect_PMV }
	else
	{ vl_PMV = vm_PMV }

	DB_CTRL_TITLE@(vm_fenetre,"LI_identification",vl_PMV.identifiant)

	vl_fmc1=SUBSTRING@(vl_PMV.fmc_liee,1,46)
	vl_fmc2=TRIM@(SUBSTRING@(vl_PMV.fmc_liee,46,20))

	vl_objet = "LI_fmc_liée_" ++ va_type
	DB_CTRL_TITLE@(vm_fenetre,vl_objet,vl_fmc1)

	vl_objet = "LI_date_fmc_" ++ va_type
	DB_CTRL_TITLE@(vm_fenetre,vl_objet,vl_fmc2)

	vl_objet = "BL_message_" ++ va_type
	DB_CTRL_HEIGHT@(vm_fenetre,vl_objet,vm_Config_PMV[vl_PMV.NumEqt].NbLigne)
	if (vm_Config_PMV[vl_PMV.NumEqt].NbCaracteres=15)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,9)
	else if (vm_Config_PMV[vl_PMV.NumEqt].NbCaracteres=18)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,12)
	vl_tplecture[0] = vl_PMV.Message1 
	vl_tplecture[1] = vl_PMV.Message2
	if (vm_Config_PMV[vl_PMV.NumEqt].NbLigne >=3)
		vl_tplecture[2] = vl_PMV.Message3
	DB_CTRL_STRINGS@(vm_fenetre,vl_objet,vl_tplecture)
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,-1)
	DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,TRUE)

	vl_tptext = vl_tplecture[0] ++ vl_tplecture[1] ++ vl_tplecture[2] ++ vl_PMV.Direction
	if TRIM@(vl_tptext)<>""
	{ 	vl_tpaffichage = CM_AFF_TEXTE }

	if vm_Config_PMV[vl_PMV.NumEqt].NbLigne = CM_4LIGNES
	{
		vl_objet = "BL_direction_" ++ va_type
		vl_tplecture[0] = vl_PMV.Direction 
		DB_CTRL_STRINGS@(vm_fenetre,vl_objet,vl_tplecture)
		DB_CTRL_VALUE@(vm_fenetre,vl_objet,-1)
		DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,TRUE)
		if (va_type = "actuel") and (vm_validation_nouv_message = XDC_FAUX)
		{ DB_CTRL_DISPLAY@(vm_fenetre,"BL_direction_nouveau",TRUE) }
	}
	else
	{
		DB_CTRL_DISPLAY@(vm_fenetre,"BL_direction_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BL_direction_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BL_alt_dir_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BL_alt_dir_actuel",FALSE)
	}

	if vm_Config_PMV[vl_PMV.NumEqt].NbLigne = CM_4LIGNES
	{
		vl_objet = "BL_alt_dir_" ++ va_type
		vl_tplecture[0] = vl_PMV.AltDirection 
		DB_CTRL_STRINGS@(vm_fenetre,vl_objet,vl_tplecture)
		DB_CTRL_VALUE@(vm_fenetre,vl_objet,-1)
	}

	vl_objet = "BL_alternat_" ++ va_type
	DB_CTRL_HEIGHT@(vm_fenetre,vl_objet,vm_Config_PMV[vl_PMV.NumEqt].NbLigne)
	if (vm_Config_PMV[vl_PMV.NumEqt].NbCaracteres=15)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,9)
	else if (vm_Config_PMV[vl_PMV.NumEqt].NbCaracteres=18)
		DB_CTRL_WIDTH@(vm_fenetre,vl_objet,12)
	vl_tplecture[0] = vl_PMV.Alternat1 
	vl_tplecture[1] = vl_PMV.Alternat2
	if (vm_Config_PMV[vl_PMV.NumEqt].NbLigne >=3)
		vl_tplecture[2] = vl_PMV.Alternat3
	DB_CTRL_STRINGS@(vm_fenetre,vl_objet,vl_tplecture)
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,-1)

	vl_tptext = vl_tplecture[0] ++ vl_tplecture[1] ++ vl_tplecture[2] ++ vl_PMV.AltDirection
	if TRIM@(vl_tptext)<>""
	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,TRUE)
		if ( va_type = "actuel" ) { DB_CTRL_DISPLAY@(vm_fenetre,"LibAlt",TRUE)}
		if vm_Config_PMV[vl_PMV.NumEqt].NbLigne = CM_4LIGNES
		{   
			vl_objet = "BL_alt_dir_" ++ va_type
		 	DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,TRUE) 
			if (va_type = "actuel") and (vm_validation_nouv_message = XDC_FAUX)
			{ 
				vl_tptext = vm_Lect_PMV.Alternat1 ++ vm_Lect_PMV.Alternat2 ++ 
						vm_Lect_PMV.Alternat3  ++ vm_Lect_PMV.AltDirection
				if TRIM@(vl_tptext)<>""
				{ DB_CTRL_DISPLAY@(vm_fenetre,"BL_alt_dir_nouveau",TRUE) }
			} 
		} 
		vl_tpaffichage = CM_AFF_TEXTE 
	}
	else
 	{ 	
		DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,False)	
		if ( va_type = "actuel" ) { DB_CTRL_DISPLAY@(vm_fenetre,"LibAlt",FALSE)}
		if vm_Config_PMV[vl_PMV.NumEqt].NbLigne = CM_4LIGNES
		{   
			vl_objet = "BL_alt_dir_" ++ va_type
		 	DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,False)
		} 
	}

	if vl_PMV.Clignotement1 = XDC_PMV_CLIGNOLENT
	{ 
		vl_tplecture = TRUE
		vl_tpaffichage = CM_AFF_TEXTE
	}
	else 
	{	vl_tplecture = FALSE }

	vl_objet = "BA_clignotement1_" ++ va_type
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,vl_tplecture)

	if vl_PMV.Clignotement2 = XDC_PMV_CLIGNOLENT
	{ 
		vl_tplecture = TRUE
		vl_tpaffichage = CM_AFF_TEXTE
	}
	else 
	{ 	vl_tplecture = FALSE }

	vl_objet = "BA_clignotement2_" ++ va_type
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,vl_tplecture)

	if vl_PMV.Clignotement3 = XDC_PMV_CLIGNOLENT
	{ 
		vl_tplecture = TRUE
		vl_tpaffichage = CM_AFF_TEXTE
	}
	else 
	{ 	vl_tplecture = FALSE }

	vl_objet = "BA_clignotement3_" ++ va_type
	DB_CTRL_VALUE@(vm_fenetre,vl_objet,vl_tplecture)

	if (vm_Config_PMV[vl_PMV.NumEqt].NbLigne=1) {
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno2",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno3",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_actuel",FALSE)
	}
	else if (vm_Config_PMV[vl_PMV.NumEqt].NbLigne=2) {
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno2",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_nouveau",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno3",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_nouveau",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_actuel",FALSE)
	}
	else if (vm_Config_PMV[vl_PMV.NumEqt].NbLigne=3) {
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno2",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno3",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_nouveau",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_nouveau",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement3_actuel",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement2_actuel",TRUE)
	}

	/*PMV avec PICTO*/

	if (vm_Config_PMV[vl_PMV.NumEqt].Picto=1) {
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno1",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BS_nota_picto",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"LI_nota_picto_actuel",TRUE)
		vl_objet = "ID_picto_xxx_" ++ va_type
		DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement_picto",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_nota_actuel",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_picto_actuel",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_picto",TRUE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno_picto_actuel",TRUE)
	if vl_PMV.ClignotementPicto = XDC_PICTO_CLIGNO 
	{ 
		vl_tplecture = TRUE
		vl_tpaffichage = CM_AFF_TEXTE
	}
	else 
	{	vl_tplecture = FALSE }

	DB_CTRL_VALUE@(vm_fenetre,"BA_clignotement_picto",vl_tplecture)

	vl_objet = "ID_picto_xxx_" ++ va_type
	for i = 0 to 30
		if TRIM@(SUBSTRING@(vl_PMV.TypePicto,1,4)) 
			= TRIM@(vm_picto[i,2]) 
		{	DB_CTRL_TITLE@(vm_fenetre,vl_objet,vm_picto[i,1])
			if vm_picto[i,2]<>"XXXX" {vl_tpaffichage = CM_AFF_TEXTE}
		}
		
	next i

	if ( va_type = "nouveau" )
	{	DB_CTRL_VALUE@(vm_fenetre,"BS_nota_picto",vl_PMV.TextePicto) }
	else
	{ 	DB_CTRL_TITLE@(vm_fenetre,"LI_nota_picto_actuel",vl_PMV.TextePicto) }

	if TRIM@(vl_PMV.TextePicto) <> "" { vl_tpaffichage = CM_AFF_TEXTE }
	}
	else {
		vl_objet = "ID_picto_xxx_" ++ va_type
		DB_CTRL_DISPLAY@(vm_fenetre,"BS_nota_picto",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"LI_nota_picto_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,vl_objet,FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_clignotement_picto",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_nota_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_picto_actuel",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"TA_picto",FALSE)
		DB_CTRL_DISPLAY@(vm_fenetre,"BA_cligno_picto_actuel",FALSE)
	}

	/*DB_EDITBOX_SET_DATA@(vm_fenetre,"BE_remarque",vl_PMV.Remarque)*/

	if ( va_type = "nouveau" )
	{ 
		if ( vm_Lect_PMV.TypeAffichage = CM_AFF_TDP or 
		    vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE ) and
		   ( vl_tpaffichage = CM_AFF_NEUTRE )
		{	vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE }
		if ( vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP and 
		    vm_Lect_PMV.TypeAffichage <> CM_AFF_TEXTE  and
		    vm_Lect_PMV.TypeAffichage <> CM_AFF_NEUTRE  and
		     vl_tpaffichage = CM_AFF_TEXTE )
		{	vm_Lect_PMV.TypeAffichage = CM_AFF_TEXTE }
	}


	if ( vm_validation_nouv_message = XDC_FAUX )
	{ 
		DB_CTRL_RETURN_ON_CHANGE@(vm_fenetre,"BL_PMV",TRUE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_lier_fmc",FALSE)
		DB_CTRL_GRAYED@(vm_fenetre,"BO_Arret",FALSE)
		DB_CTRL_GRAYED@(vm_fenetre,"BS_nota_picto",FALSE)
		DB_CTRL_GRAYED@(vm_fenetre,"BP_copier_coller",FALSE)
	}

	RETURN (COM_OK)
	
ENDMACRO



/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Commande du nouveau PMV
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Commande_PMV

/*
* ARGUMENTS EN ENTREE : 
*	aucun
*
*
* ARGUMENTS EN SORTIE : aucun
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*   	Commande du nouveau PMV.
*
---------------------------------------------------------------------------- */

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats
	
	var		vl_tptext		' zone de text tampon

	/*A test les droits d'acces à cette fonction */
	if NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_DISTRICT)) and 
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_PC_SIMPLIFIE)) and
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_PC2)) and
	(NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_CI) and    
	 SYSTEM_VAR@(vg_site) = XDC_CI) )
	{
		ITMA_TPM_Acces_interdit
		RETURN(COM_NOK) 
	}


	vm_Lect_PMV.Horodate = COM09_Date_Courante(TRUE)
	vm_Lect_PMV.Operateur = SUBSTRING@(vm_NomOperateur, 1, 25)

	if NOT IS_NUMERIC_STRING@(vm_Lect_PMV.NumEqt)
	{
		vl_tptext ="Commande_PMV refusée (numéro d'équipement invalide)"
		COM01_Trace(COM_WARNING,vl_tptext)
		RETURN(COM_NOK)
	}

	vm_Lect_PMV.Operateur = SYSTEM_VAR@("vg_numero_poste")
	vm_Lect_PMV.Flash = XDC_PMV_PASFLASH

	vl_parametres[0].type	= SYB#DATETIME_
	vl_parametres[0].data	= vm_Lect_PMV.Horodate
	vl_parametres[0].output	= FALSE

	vl_parametres[1].type	= SYB#INT4_
	vl_parametres[1].data	= vm_Lect_PMV.Numero_Evt
	vl_parametres[1].output	= FALSE

	vl_parametres[2].type	= SYB#INT1_
	vl_parametres[2].data	= vm_Lect_PMV.Cle_Evt
	vl_parametres[2].output	= FALSE

	vl_parametres[3].type	= SYB#INT2_
	vl_parametres[3].data	= vm_Lect_PMV.Operateur
	vl_parametres[3].output	= FALSE

	vl_parametres[4].type	= SYB#INT2_
	vl_parametres[4].data	= vm_Lect_PMV.NumEqt
	vl_parametres[4].output	= FALSE

	vl_parametres[5].type	= SYB#INT4_
	vl_parametres[5].data	= vm_Config_PMV[vm_Lect_PMV.NumEqt].sitegestion
	vl_parametres[5].output	= FALSE

	vl_parametres[6].type	= SYB#CHAR_
	vl_parametres[6].data	= vm_Lect_PMV.Message1
	vl_parametres[6].output	= FALSE

	vl_parametres[7].type	= SYB#CHAR_
	vl_parametres[7].data	= vm_Lect_PMV.Message2
	vl_parametres[7].output	= FALSE

	vl_parametres[8].type	= SYB#CHAR_
	vl_parametres[8].data	= vm_Lect_PMV.Message3
	vl_parametres[8].output	= FALSE

	vl_parametres[9].type	= SYB#CHAR_
	vl_parametres[9].data	= vm_Lect_PMV.Alternat1
	vl_parametres[9].output	= FALSE

	vl_parametres[10].type	= SYB#CHAR_
	vl_parametres[10].data	= vm_Lect_PMV.Alternat2
	vl_parametres[10].output	= FALSE

	vl_parametres[11].type	= SYB#CHAR_
	vl_parametres[11].data	= vm_Lect_PMV.Alternat3
	vl_parametres[11].output	= FALSE

	vl_parametres[12].type	= SYB#INT1_
	vl_parametres[12].data	= vm_Lect_PMV.Clignotement1
	vl_parametres[12].output	= FALSE

	vl_parametres[13].type	= SYB#INT1_
	vl_parametres[13].data	= vm_Lect_PMV.Clignotement2
	vl_parametres[13].output	= FALSE

	vl_parametres[14].type	= SYB#INT1_
	vl_parametres[14].data	= vm_Lect_PMV.Clignotement3
	vl_parametres[14].output	= FALSE

	vl_parametres[15].type	= SYB#INT1_
	vl_parametres[15].data	= vm_Lect_PMV.Flash
	vl_parametres[15].output	= FALSE

	vl_parametres[16].type	= SYB#CHAR_
	vl_parametres[16].data	= vm_Lect_PMV.TypePicto
	vl_parametres[16].output	= FALSE

	vl_parametres[17].type	= SYB#CHAR_
	vl_parametres[17].data	= vm_Lect_PMV.TextePicto
	vl_parametres[17].output	= FALSE

	vl_parametres[18].type	= SYB#INT1_
	vl_parametres[18].data	= vm_Lect_PMV.ClignotementPicto
	vl_parametres[18].output	= FALSE

	vl_parametres[19].type	= SYB#INT1_
	vl_parametres[19].data	= vm_Lect_PMV.Priorite
	vl_parametres[19].output	= FALSE

	vl_parametres[20].type	= SYB#INT4_
	vl_parametres[20].data	= vm_Lect_PMV.DistanceEvt
	vl_parametres[20].output	= FALSE

	vl_parametres[21].type	= SYB#CHAR_
	if vm_Config_PMV[vm_Lect_PMV.NumEqt].NbLigne = CM_4LIGNES
	{	vl_parametres[21].data	= vm_Lect_PMV.Direction }	' Ligne direction
	else
	{	vl_parametres[21].data	= XDC_CHAINE_VIDE}			' Ligne direction
	vl_parametres[21].output	= FALSE

	vl_parametres[22].type	= SYB#CHAR_
	if vm_Config_PMV[vm_Lect_PMV.NumEqt].NbLigne = CM_4LIGNES
	{	vl_parametres[22].data	= vm_Lect_PMV.AltDirection }	' Alternatdirection
	else
	{	vl_parametres[22].data	= XDC_CHAINE_VIDE}			' Alternatdirection
	vl_parametres[22].output	= FALSE

	vl_parametres[23].type	= SYB#INT2_
	vl_parametres[23].data	= vm_Lect_PMV.Destination	' destination
	vl_parametres[23].output	= FALSE

	vl_parametres[24].type	= SYB#INT2_
	vl_parametres[24].data	= vm_Lect_PMV.AltDestination 'Destination alternat
	vl_parametres[24].output	= FALSE

	vl_parametres[25].type	= SYB#INT1_
	vl_parametres[25].data	= CM_NO_SEUIL			 'Depassement seuil
	vl_parametres[25].output	= FALSE

	vl_parametres[26].type  = SYB#INT4_
	vl_parametres[26].data  = SYSTEM_VAR@(vg_site)
	vl_parametres[26].output        = FALSE

	if ( vm_Lect_PMV.TypeAffichage <> CM_AFF_TDP ) or ( vm_Lect_PMV.TypePicto <>  XDC_PICTO_PASPICTO)

	{
		vl_parametres[27].type	= SYB#INT4_
		vl_parametres[27].data	= 0
		vl_parametres[27].output	= TRUE
		
		vl_parametres[28].type	= SYB#INT2_
		vl_parametres[28].data	= vm_Lect_PMV.DestinationNoeud 'Destination noeud
		vl_parametres[28].output	= FALSE


		IF COM05_SQL_Procedure
			(XZAC01_Commande_PMV,vl_parametres,vl_resultats,C_MODULE) <> COM_OK
		{
			vl_tptext = "Status : "
			DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
			RETURN (COM_NOK)
		}

		vm_Lect_PMV.NumeroAction = vl_resultats.return_parameters[0]
	}
	else
	{
		vl_parametres[27].type	= SYB#INT2_
		vl_parametres[27].data	= vm_Lect_PMV.DestinationNoeud 'Destination noeud
		vl_parametres[27].output	= FALSE

		IF COM05_SQL_Procedure
			(XZAC611_Commande_PMV_TDP,vl_parametres,vl_resultats,C_MODULE) <> COM_OK
		{
			vl_tptext = "Status : "
			DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
			RETURN (COM_NOK)
		}
  	}

	vl_tptext = "Status : Commande envoyee ..."
 	DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

	RETURN (COM_OK)

ENDMACRO 


/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Arret du PMV
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Commande_Fin_PMV

/*
* ARGUMENTS EN ENTREE : 
*	aucun
*
*
* ARGUMENTS EN SORTIE : aucun
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*    Arret du PMV.
*
---------------------------------------------------------------------------- */

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats
	

	VAR		vl_heure
	var		vl_tptext		' zone de text tampon
	var		vl_type_extinct
	VAR		vl_cr

	/*A test les droits d'acces à cette fonction */
	if NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_DISTRICT)) and 
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_PC_SIMPLIFIE)) and
	NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_PC2)) and
	(NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_CI) and 	
	 SYSTEM_VAR@(vg_site) = XDC_CI) )
	{
		ITMA_TPM_Acces_interdit
		RETURN(COM_NOK)
	}

	vl_heure = COM09_Date_Courante (TRUE)

	if (vm_Lect_PMV.TypeAffichage = CM_AFF_NEUTRE) vl_type_extinct = XDC_PMV_DEFAUT
	if (vm_Lect_PMV.TypeAffichage = CM_AFF_HEURE) vl_type_extinct = XDC_PMV_HEURE
	if (vm_Lect_PMV.TypeAffichage = CM_AFF_TEMPER) vl_type_extinct = XDC_PMV_TEMPERATURE
	if (vm_Lect_PMV.TypeAffichage = CM_AFF_ETEINT) vl_type_extinct = XDC_PMV_ETEINDRE

	vl_parametres[0].type	= SYB#INT4_
	vl_parametres[0].data	= vm_Lect_PMV.NumEqt
	vl_parametres[0].output	= FALSE

	vl_parametres[1].type	= SYB#INT4_
	vl_parametres[1].data	= vm_Config_PMV[vm_Lect_PMV.NumEqt].sitegestion
	vl_parametres[1].output	= FALSE

	vl_parametres[2].type	= SYB#DATETIME_
	vl_parametres[2].data	= vl_heure
	vl_parametres[2].output	= FALSE

	vl_parametres[3].type	= SYB#INT2_
	vl_parametres[3].data	= vl_type_extinct
	vl_parametres[3].output	= FALSE

	vl_parametres[4].type = SYB#CHAR_                               ' Site local
	vl_parametres[4].data = SYSTEM_VAR@(vg_nom_site)
	vl_parametres[4].output = FALSE

	vl_tptext =  " Extinction PMV numéro " ++ vm_Lect_PMV.NumEqt ++ ", type : "++ vl_type_extinct
     COM01_Trace(COM_INFO,vl_tptext)

	vl_tptext = "Status : Arrêt demandé ..."
 	DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

	IF (vl_type_extinct = XDC_PMV_DEFAUT)
	{
		vl_cr = COM05_SQL_Procedure (XZAC610_Mise_Au_Neutre_PMV,
				COM15_Parametres_Requete ( {
					{ FALSE, SYB#INT2_, vm_Lect_PMV.NumEqt },
					{ FALSE, SYB#INT1_, vm_Config_PMV[vm_Lect_PMV.NumEqt].sitegestion },
					{ FALSE, SYB#INT1_, XDC_VRAI },
					{ FALSE, SYB#CHAR_, XDC_CHAINE_VIDE },
					{ FALSE, SYB#CHAR_, XDC_CHAINE_VIDE },
					{ FALSE, SYB#CHAR_, XDC_CHAINE_VIDE },
					{ FALSE, SYB#INT2_, XDC_DTP_AUCUNE },
					{ FALSE, SYB#INT2_, XDC_DTP_AUCUNE },
					{ FALSE, SYB#INT2_, XDC_DTP_AUCUNE },
					{ TRUE, SYB#INT4_, NULL },
					{ TRUE, SYB#INT1_, NULL },
					{ FALSE, SYB#INT2_, XDC_DTP_AUCUNE },
                                        { FALSE, SYB#CHAR_, SYSTEM_VAR@(vg_nom_site) }
				} ), vl_resultats, C_MODULE)
		vm_Lect_PMV.NumeroAction = vl_resultats.return_parameters[0]
	}
	ELSE
	{
		vl_cr = COM05_SQL_Procedure (XZAC61_Commande_Fin_PMV,vl_parametres,vl_resultats,C_MODULE)
		vm_Lect_PMV.NumeroAction = 0
	}

	IF (vl_cr <> COM_OK)
	{
		vl_tptext = "Status : erreur arret"
 		DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)
		RETURN (COM_NOK)
	}

	vl_tptext = "Status : arret PMV effectue..."
 	DB_CTRL_TITLE@(vm_fenetre,"LI_status",vl_tptext)

	RETURN (COM_OK)

ENDMACRO 




/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Retourne un message à l'operateur et trace l'accès interdit
*				à une macro.
*
* ----------------------------------------------------------------------------*/

MACRO ITMA_TPM_Acces_interdit(va_NomMacro)

/*-----------------------------------------------------------------------------
* ARGUMENTS EN ENTREE : 
*		va_NomMacro	:	le nom de la macro interdite
*
* ARGUMENTS EN SORTIE : aucun
*
* CODE RETOUR	: COM_OK en cas d'execution correcte, COM_NOK sinon.

* CONDITION D'UTILISATION :
*   appel d'une macro avec les droits d'accès insuffisant.
*
* FONCTION :
*	Retourne un message à l'operateur et trace l'accès interdit à une macro
*---------------------------------------------------------------------------- */

	/*A message d'info à l'opérateur */
	var		vl_tptext			' zone de text tampon
	
	vl_tptext = "L'utilisateur " ++vm_NomOperateur ++ " n'a pas les droits suffisants" ++ NUM_TO_STRING@(10) ++ "pour piloter un PMV"
	info_message@(vl_tptext)

	/*A trace de l'appel de procedure */
	vl_tptext =  " Accès Macro "
				 ++ va_NomMacro ++ " refusé pour " ++ vm_NomOperateur
	COM01_Trace(COM_WARNING,vl_tptext)

ENDMACRO




/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Lecture des équipements disponibles
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Liste_Eqt_Dispo

/*
* ARGUMENTS EN ENTREE : 
*		aucun
*
* ARGUMENTS EN SORTIE : 
*		ListeEqts		: la liste des équipements correspondants
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*   	Lecture des équipements en disponibilités.
*
---------------------------------------------------------------------------- */
	var	FORMAT COM_Donnees_Equipements vl_info_equip
	var	vl_Liste		' liste formattée
	var	i,k			' index de loop
	var vl_district

	if vm_District = XDC_CI
	{	vl_District	= NULL	}
	else
	{	vl_District	= vm_District	}

	ITMA_COM_Lire_Equipements (XDC_EQT_PMV, vl_District,
				 XDC_EQT_MINEUR, XDC_EQT_MINEUR,
				 XDC_EQT_HS + XDC_EQT_MAJEUR + XDC_EQT_CRITIQUE + XDC_EQT_DESACTIVE + XDC_EQT_INHIBE, 0,vl_info_equip,
			C_MODULE)

	/*A Construction de la liste des équipements */
	k = 0
	if  IS_ARRAY@(vl_info_equip)
	{
	   for i = 0 to ARRAY_SIZE@(vl_info_equip) - 1	
			if (vl_info_equip[i].sitegestion = vm_District) OR 
			   ((vm_District = XDC_CI) and 
					(ITMA_COM_District_pilotable( 	
						vl_info_equip[i].sitegestion, 
							XDC_LIB_PMV,FALSE,NULL)) )
			{
			vl_Liste[k] = vm_Config_PMV[vl_info_equip[i].numero].Identifiant
			k= k + 1
			}
	    
		next i
	}
	else
	{
		vl_Liste[0]		= 	""
		info_message@("Pas de PMV diponible dans cette région...")
	}

	for i = 0 to ARRAY_SIZE@(vm_Config_PMV)-1
	   if (vm_Config_PMV[i].sitegestion = vm_District) or 
		 ( (vm_District = XDC_CI) and 
		 (ITMA_COM_District_pilotable(vm_Config_PMV[i].sitegestion, 
			XDC_LIB_PMV,FALSE,NULL)) )
	   {
		vm_etat_PMV[i].Horodate 			=	COM09_Date_Courante(TRUE)
		vm_etat_PMV[i].NumEqt 			=	i		
		vm_etat_PMV[i].Operateur 		= 	""			
		vm_etat_PMV[i].Identifiant 		= 	vm_Config_PMV[i].Identifiant			
		vm_etat_PMV[i].DispoPMV 			= 	0			
		vm_etat_PMV[i].DispoPicto 		= 	0				
		vm_etat_PMV[i].NumTypePMV		=	0					
  	 	vm_etat_PMV[i].Message1			=	""					
  	 	vm_etat_PMV[i].Message2			=	""					
  	 	vm_etat_PMV[i].Message3			=	""					
		vm_etat_PMV[i].Alternat1			=	""					
		vm_etat_PMV[i].Alternat2			=	""				
		vm_etat_PMV[i].Alternat3			=	""				
  		vm_etat_PMV[i].Flash			=	XDC_PMV_PASFLASH			
  	 	vm_etat_PMV[i].Clignotement1		=	XDC_PMV_PASCLIGNO	
  		vm_etat_PMV[i].Clignotement2		=	XDC_PMV_PASCLIGNO	
    		vm_etat_PMV[i].Clignotement3		=	XDC_PMV_PASCLIGNO	
		vm_etat_PMV[i].ClignotementPicto	=	XDC_PMV_PASCLIGNO	
		vm_etat_PMV[i].TypePicto			=	"XXXX"				
		vm_etat_PMV[i].TextePicto		=	""			
		vm_etat_PMV[i].NumTypePicto		=	0		
		vm_etat_PMV[i].fmc_liee			=	""			
		vm_etat_PMV[i].Remarque			=	""			
		vm_etat_PMV[i].Explication		=	""			
		vm_etat_PMV[i].Numero_Evt		=	0		
		vm_etat_PMV[i].Cle_Evt			=	0			
		vm_etat_PMV[i].Priorite			=	0			
		vm_etat_PMV[i].DistanceEvt		=	XDC_DISTANCE_INCONNUE		
		vm_etat_PMV[i].NumeroAction		=	0	
  	 	vm_etat_PMV[i].Direction			=	""					
  	 	vm_etat_PMV[i].AltDirection		=	""					
  	 	vm_etat_PMV[i].Destination		=	0					
  	 	vm_etat_PMV[i].AltDestination		=	0					
  	 	vm_etat_PMV[i].TypeAffichage		=	CM_AFF_TEXTE					
  	 	vm_etat_PMV[i].DestinationNoeud		=	0
		vm_etat_PMV[i].SiteGestion		=	vm_Config_PMV[i].SiteGestion
	  }
	next i
	RETURN (vl_Liste)

ENDMACRO


 


/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Reset etat du PMV actuel ou nouveau
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Reset_PMV ( va_type )

/*
* ARGUMENTS EN ENTREE : 
*	va_type : actuel ou nouveau
*
* ARGUMENTS EN SORTIE : 
*	aucun
*
*
* CONDITION D'UTILISATION
*   ITMA_TPM_pilotage_d_un_PMV
*
*
* FONCTION
*   	Lecture des équipements en disponibilités.
*
---------------------------------------------------------------------------- */

	var	format type_PMV_GEN	vl_PMV

	if ( va_type = "nouveau" )
	{ vl_PMV = vm_Lect_PMV }
	else
	{ vl_PMV = vm_PMV }

	vl_PMV.Horodate 		= COM09_Date_Courante(TRUE)

	vl_PMV.Operateur 		= ""			
	vl_PMV.Identifiant 		= vm_Config_PMV[vl_PMV.NumEqt].Identifiant			
	vl_PMV.DispoPMV 		= 0			
	vl_PMV.DispoPicto 		= 0				
	vl_PMV.NumTypePMV		= 0					
  	vl_PMV.Message1		= ""					
  	vl_PMV.Message2		= ""					
  	vl_PMV.Message3		= ""					
	vl_PMV.Alternat1		= ""					
	vl_PMV.Alternat2		= ""				
	vl_PMV.Alternat3		= ""				
  	vl_PMV.Flash			= XDC_PMV_PASFLASH			
  	vl_PMV.Clignotement1	= XDC_PMV_PASCLIGNO	
  	vl_PMV.Clignotement2	= XDC_PMV_PASCLIGNO	
    	vl_PMV.Clignotement3	= XDC_PMV_PASCLIGNO	
	vl_PMV.ClignotementPicto	= XDC_PMV_PASCLIGNO	
	vl_PMV.TypePicto		= "XXXX"				
	vl_PMV.TextePicto		= ""			
	vl_PMV.NumTypePicto		= 0		
	vl_PMV.fmc_liee		= ""			
	vl_PMV.Remarque		= ""			
	vl_PMV.Explication		= ""			
	vl_PMV.Numero_Evt		= 0		
	vl_PMV.Cle_Evt			= 0			
	vl_PMV.Priorite		= 0			
	vl_PMV.DistanceEvt		= XDC_DISTANCE_INCONNUE
	vl_PMV.NumeroAction		= 0	
  	vl_PMV.Direction		= ""					
  	vl_PMV.AltDirection		= ""					
  	vl_PMV.Destination		=	0					
  	vl_PMV.AltDestination	=	0					
  	vl_PMV.TypeAffichage	=	CM_AFF_TEXTE					
  	vl_PMV.DestinationNoeud	=	0					

	if ( va_type = "nouveau" )
	{ vm_Lect_PMV = vl_PMV }
	else
	{ vm_PMV = vl_PMV }

ENDMACRO





/*X*/
/* ----------------------------------------------------------------------------
* SERVICE RENDU :	Lit en base et affiche la mise au neutre programmee
*                       du nouveau PMV a la date courante, s'il y en a une.
*
*  ----------------------------------------------------------------------------
*/

MACRO ITMA_TPM_Lire_Neutre_Programme ()

	VAR	FORMAT SQL_Procedure_Params@	vl_parametres
	VAR	FORMAT SQL_Procedure_Result@	vl_resultats

	VAR	FORMAT XZATT_Neutre_PMV		tl_Neutres
	VAR	vl_cr

	vl_parametres = COM15_Parametres_Requete ( {
				{ FALSE, SYB#INT2_, vm_Lect_PMV.NumEqt },
				{ TRUE, COM_Numero_Evenement, 0 },
				{ TRUE, COM_Cle_Evenement, 0 },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },		/* Picto */
				{ TRUE, SYB#CHAR_, XDC_CHAINE_VIDE },		/* Bandeau */
				{ TRUE, SYB#INT1_, NULL }
			} )

	vl_cr = COM05_SQL_Procedure (XZAT63_Neutre_Programme_PMV, vl_parametres, vl_resultats, C_MODULE)
	IF (vl_cr <> COM_OK)
		RETURN (COM_NOK)

	tl_Neutres[0] = vl_resultats.return_parameters
	IF (tl_Neutres[0].numero_fmc = 0)
		RETURN (COM_OK)

	ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_1, vm_Lect_PMV.Message1, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_2, vm_Lect_PMV.Message2, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
	ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_3, vm_Lect_PMV.Message3, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)

	vm_Lect_PMV.TypePicto = tl_Neutres[0].pictogramme
	vm_Lect_PMV.TextePicto = tl_Neutres[0].bandeau

	IF (tl_Neutres[0].mode_affichage = XDC_PMV_MODE_CLIGNO) {
		vm_Lect_PMV.Clignotement1 = XDC_PMV_CLIGNOLENT
		vm_Lect_PMV.Clignotement2 = XDC_PMV_CLIGNOLENT
		vm_Lect_PMV.Clignotement3 = XDC_PMV_CLIGNOLENT
	} else {
		vm_Lect_PMV.Clignotement1 = XDC_PMV_PASCLIGNO
		vm_Lect_PMV.Clignotement2 = XDC_PMV_PASCLIGNO
		vm_Lect_PMV.Clignotement3 = XDC_PMV_PASCLIGNO
	}
	vm_Lect_PMV.ClignotementPicto = XDC_PICTO_PASCLIGNO

	IF (tl_Neutres[0].mode_affichage = XDC_PMV_MODE_ALTERNE) {
		ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_1_alternat, vm_Lect_PMV.Alternat1, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
		ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_2_alternat, vm_Lect_PMV.Alternat2, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
		ITMA_TPM_Tabuler_ligne (tl_Neutres[0].ligne_3_alternat, vm_Lect_PMV.Alternat3, vm_Config_PMV[vm_Lect_PMV.NumEqt].NbCaracteres)
	} else {
		vm_Lect_PMV.Alternat1 = ""
		vm_Lect_PMV.Alternat2 = ""
		vm_Lect_PMV.Alternat3 = ""
	}

	RETURN (COM_OK)
ENDMACRO
