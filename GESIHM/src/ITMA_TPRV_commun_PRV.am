/*E*/
/*  Fichier : $Id: ITMA_TPRV_commun_PRV.am,v 1.2 2017/04/25 11:08:30 devgfi Exp $      Release : $Revision: 1.2 $        Date : $Date: 2017/04/25 11:08:30 $
------------------------------------------------------------------------
* GTIE *  PROJET MIGRAZUR
------------------------------------------------------------------------
* SOUS-SYSTEME  GESIHM
------------------------------------------------------------------------
* MODULE MTPM * FICHIER  ITMA_TPRV_commun_PRV.am
------------------------------------------------------------------------
* DESCRIPTION DU MODULE :
*
* IHM de commande des PRV.
*
*
------------------------------------------------------------------------
* HISTORIQUE :
*
* LCL		20/04/2012	: Creation (DEM/1016)			1.1
* JPL		23/03/2017	: Changement du terme Secteur en Region (DEM 1173)  1.2
--------------------------------------------------------------------- */


/*A Description des modules a inclures 
* ------------------------------------ */
INCLUDE	"dbase_.am"
INCLUDE	"../inc/ITMA_COM.h"
INCLUDE	"../inc/ITMA_TDO.h"
INCLUDE	"../inc/ITMA_TAR.h"
INCLUDE	"../../XDMICG/inc/xdc_ax.h"
INCLUDE	"../../XDMICG/inc/xzic_ax.h"
INCLUDE	"../inc/ITMA_TPRV.h"


 /*A Description des procedures stockees a appeler
* ------------------------------------------------ */
INCLUDE "../inc/xzac802sp.h"
INCLUDE "../inc/xzac805sp.h"
INCLUDE "../inc/xzac303sp.h"
INCLUDE "../inc/xzat51sp.h"
INCLUDE "../inc/xzao870sp.h"
INCLUDE "../inc/xzao05sp.h"
/*
INCLUDE "../inc/xzao422sp.h"
INCLUDE "../inc/xzac67sp.h"
INCLUDE "../inc/xzac68sp.h"
INCLUDE "../inc/xzac66sp.h"
INCLUDE "../inc/xzap130sp.h"
INCLUDE "../inc/xzao05sp.h"
INCLUDE "../inc/xzat28sp.h"
*/

var	vm_indice_objet_consultation
var	vm_indice_objet_pilotage
var	vm_chrono_dessin
var	FORMAT Config_PRV	vm_liste_PRV

/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Initialisation du PRV
* 
*  ---------------------------------------------------------------------
* SEQUENCE D'APPEL :
*/

MACRO ITMA_TPRV_commun_PRV()


ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Initialisation du PRV
* 
*  ---------------------------------------------------------------------
* SEQUENCE D'APPEL :
*/

MACRO ITMA_TPRV_INIT_PRV(	)

/*
* ARGUMENTS EN ENTREE : 
*	va_ST_aff_msg	:	Structure contenant les donnees d'affichage
*					et de messages vers les equipements
*	va_liste_objet_consultation	:	Liste des objets consultation
*	va_liste_objet_pilotage		:	Liste des objets pilotage
*	va_TypePRV	:	Type du PRV en cours
*
* ARGUMENTS EN SORTIE : Aucun
*
* CODE RETOUR         : COM_OK
*
* CONDITION D'UTILISATION
*
* FONCTION
*
--------------------------------------------------------------------- */

info_message@("A coder : ITMA_TPRV_INIT_PRV")

ENDMACRO



/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Traitement du fichier configuration du PRV
* 
*  ---------------------------------------------------------------------
* SEQUENCE D'APPEL :
*/





/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Commander le sequence d'une voie vers SYBASE
*
* --------------------------------------------------------------------*/

MACRO ITMA_TPRV_Commande_PRV(	va_PosteOperateur,
								va_NumEvt,
								va_CleEvt,
								va_NumPRV,
								FORMAT PA_PRV va_PRV,
								va_Bandeau,
								va_Vitesse,
								va_NomSite,
								va_numAction)

/*----------------------------------------------------------------------
* ARGUMENTS EN ENTREE :
* 
*		va_PosteOperateur		:	le nom du poste operateur
*		va_NumEvt				:	Numero de l'evenement
*		va_CleEvt				:	Cle de l'evenement
*		va_NumPRV				:	Numero du PRV en cours
*		va_Bandeau				:	Bandeau a afficher
*		va_Vitesse				:	Vitesse a afficher
*		va_NomSite				: 	Site
*
* ARGUMENTS EN SORTIE : aucun
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
* CONDITION D'UTILISATION
*   		ITMA_TPP_pilotage_PRV
*
* FONCTION
*   		Commander le sequence d'une voie vers SYBASE.
*
--------------------------------------------------------------------- */

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats

	var	vl_Heure			' heure courante base
	var	vl_text

	vl_Heure = COM09_Date_Courante()

	/*A trace de l'appel de la procedure */
	vl_text = " ---> Appel de la procedure 'XZAC802_Commande_PRV("
					++va_PosteOperateur++")'"
	COM01_Trace(0,vl_text)

	/*A parametrage de vl_parametres */
	vl_parametres[0].type = SYB#INT4_
	vl_parametres[0].data = va_NumEvt			' NumEvt
	vl_parametres[0].output = FALSE

	vl_parametres[1].type = SYB#INT1_
	vl_parametres[1].data = va_CleEvt			' CleEvt
	vl_parametres[1].output = FALSE

	vl_parametres[2].type = SYB#INT4_
	vl_parametres[2].data = va_PosteOperateur	' Poste Operateur
	vl_parametres[2].output = FALSE

	vl_parametres[3].type = SYB#INT4_
	vl_parametres[3].data = va_NumPRV			' NumEqt
	vl_parametres[3].output = FALSE

	vl_parametres[4].type = SYB#DATETIME_
	vl_parametres[4].data = vl_heure			' Heure de lancement
	vl_parametres[4].output = FALSE

	vl_parametres[5].type = SYB#INT1_
	vl_parametres[5].data = va_NomSite			' Nom du site
	vl_parametres[5].output = FALSE

	vl_parametres[6].type = SYB#CHAR_
	vl_parametres[6].data = va_Bandeau			' Texte du bandeau
	vl_parametres[6].output = FALSE

	vl_parametres[7].type = SYB#CHAR_
	vl_parametres[7].data = va_Vitesse			' Vitesse
	vl_parametres[7].output = FALSE

	vl_parametres[8].type = SYB#INT1_
	vl_parametres[8].data = va_PRV.Priorite		' Priorite
	vl_parametres[8].output = FALSE

	vl_parametres[9].type = SYB#INT4_
	vl_parametres[9].data = va_PRV.DistanceEvt	' DistanceEvt
	vl_parametres[9].output = FALSE

	vl_parametres[10].type = SYB#INT4_
	vl_parametres[10].data = SYSTEM_VAR@(vg_site)  		'site local
	vl_parametres[10].output = FALSE

	IF COM05_SQL_Procedure (XZAC802_Commande_PRV,
				vl_parametres,vl_resultats,C_MODULE) <> COM_OK
	{
		RETURN (COM_NOK)
	}
	va_NumAction = vl_resultats.return_parameters[0]
	COM01_Trace(0," ")

	/*A retour du Num d'action */
	RETURN (COM_OK)

ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Retour a l'etat normal de la PAL
*
* --------------------------------------------------------------------*/

MACRO ITMA_TPRV_Commande_fin_PRV(	va_PosteOperateur,
									va_NumEvt,
									va_CleEvt,
									va_NumEqt,
									FORMAT PA_PRV va_PRV,
									va_Bandeau,
									va_Vitesse,
									va_NomSite,
									va_NomOperateur,
									va_NumAction)

								

/*----------------------------------------------------------------------
* ARGUMENTS EN ENTREE :
* 
*		va_PosteOperateur		:	Le poste de l'operateur
*		va_NumEvt				:	Num FMC
*		va_CleEvt				:	Cle FMC
*		va_NumEqt				:	le numero d'equipement
*		va_Bandeau				: 	Le bandeau a afficher
*		va_Vitesse				: 	La vitesse a afficher
*		va_NomSite				:	Nom du site
*		va_NomOperateur		:	Le nom de l'operateur
*
* ARGUMENTS EN SORTIE : aucun
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
* CONDITION D'UTILISATION
*   		ITMA_TPRV_pilotage_PRV
*
* FONCTION
*   		Retour a l'etat normal du PRV
*
--------------------------------------------------------------------- */

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats

	var	vl_Heure			' heure courante
	var	vl_text

	
	if NOT(COM04_Operateur_A_Le_Droit_De( XDC_FAM_EXPLOITATION_DISTRICT)) and 
		NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_PC_SIMPLIFIE)) and
		NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_PC2)) and
		NOT(COM04_Operateur_A_Le_Droit_De(XDC_FAM_EXPLOITATION_CI))
	{
		ITMA_TPRV_Acces_interdit("ITMA_TPRV_Commande_fin_PRV",
											va_NomOperateur)
		RETURN(COM_NOK)
	}

	vl_Heure = COM09_Date_Courante()

	/*A trace de l'appel de la procedure */
	vl_text =
	" ---> Appel de la procedure'XZAC303_Commande_Fin_PRV("
					++va_NumEqt++","++va_NomSite++","
						++vl_Heure++")'"
	COM01_Trace(0,vl_text)

	/*A parametrage de vl_parametres */
	vl_parametres[0].type = SYB#INT4_
	vl_parametres[0].data = va_NumEvt			' NumEvt
	vl_parametres[0].output = FALSE

	vl_parametres[1].type = SYB#INT1_
	vl_parametres[1].data = va_CleEvt			' CleEvt
	vl_parametres[1].output = FALSE

	vl_parametres[2].type = SYB#INT4_
	vl_parametres[2].data = va_PosteOperateur	' Poste Operateur
	vl_parametres[2].output = FALSE

	vl_parametres[3].type = SYB#INT4_
	vl_parametres[3].data = va_NumEqt			' NumEqt
	vl_parametres[3].output = FALSE

	vl_parametres[4].type = SYB#DATETIME_
	vl_parametres[4].data = vl_heure			' Heure de lancement
	vl_parametres[4].output = FALSE

	vl_parametres[5].type = SYB#INT1_
	vl_parametres[5].data = va_NomSite			' Nom du site
	vl_parametres[5].output = FALSE

	vl_parametres[6].type = SYB#CHAR_
	vl_parametres[6].data = va_Bandeau			' Texte du bandeau
	vl_parametres[6].output = FALSE

	vl_parametres[7].type = SYB#CHAR_
	vl_parametres[7].data = va_Vitesse			' Vitesse
	vl_parametres[7].output = FALSE

	vl_parametres[8].type = SYB#INT1_
	vl_parametres[8].data = va_PRV.Priorite		' Priorite
	vl_parametres[8].output = FALSE

	vl_parametres[9].type = SYB#INT4_
	vl_parametres[9].data = va_PRV.DistanceEvt	' DistanceEvt
	vl_parametres[9].output = FALSE

	vl_parametres[10].type = SYB#INT4_
	vl_parametres[10].data = SYSTEM_VAR@(vg_site)  		'site local
	vl_parametres[10].output = FALSE


	IF COM05_SQL_Procedure(XZAC303_Commande_Fin_PRV,
				vl_parametres,vl_resultats,C_MODULE) <> COM_OK
	{
		RETURN (COM_NOK)
	}
	va_NumAction = vl_resultats.return_parameters[0]

	/*A retour OK */
	RETURN (COM_OK)

ENDMACRO



/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Permet de s'abonner a l'etat des panneaux de la base.
*
* --------------------------------------------------------------------*/

MACRO ITMA_TPRV_Abt_Etat_Panneaux_PRV(va_Abonnement,va_NomSite)

/*----------------------------------------------------------------------
* ARGUMENTS EN ENTREE : 
*
*		va_Abonnement	:	Type d'abonnement (debut ou fin)
*		va_NomSite		:	Nom du site
*
* ARGUMENTS EN SORTIE : aucun
*
* CODE RETOUR	: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
* CONDITION D'UTILISATION :
*   ITMA_TCA_pilotage_camera
*
* FONCTION :
*	Permet de s'abonner a l'etat des panneaux PRV.
* ------------------------------------------------------------------- */

	VAR vl_cmd
	VAR vl_text

	/*A trace de l'appel de procedure */
	vl_text = " --> XZEA081_Abt_Etat_PRV "
	COM01_Trace(0,vl_text)

	/*B preparation de la requete pour le coupleur */
	vl_cmd[0]=MTAR_MSG_EXEC

	/*B num de commande pour retour. Non utilise ici */
	vl_cmd[1]=1			

	/*B fonction a executer */
	vl_cmd[2]=MTAR_FCT_XZEA081  

	/*B arguments */
	vl_cmd[3]= va_Abonnement ++ MTAR_CAR_SEPAR ++ va_NomSite

	/*B envoi de la commande a la tache ITMA_TAR01 */
	DB_SEND_POKE@(COM_CANAL_TAR01,vl_cmd)
	
	/*B trace de l'emmission */
	vl_text = " - DB_SEND_POKE@(COM_CANAL_TAR01," ++ vl_cmd[3] ++ ")"
	COM01_Trace(0,vl_text)
	COM01_Trace(0," ")

	/*B code retour OK */
	RETURN(COM_OK)

ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Lecture d'une action PRV a partir du num de PA
*
*  ---------------------------------------------------------------------
*/

MACRO ITMA_TPRV_Lecture_PA_PRV(va_NumPA,va_site)

/*
* ARGUMENTS EN ENTREE 	:
*   va_NumPA       	: Num du Plan d'action 
*   va_site    	: le numero du site 
*
*
* ARGUMENTS EN SORTIE	: 
*	PRV[va_type]	: etat du PRV
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_PRV
*
*
--------------------------------------------------------------------- */


	VAR 	i,j				' index de boucle
	VAR	va_text
	VAR	vl_retour

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats
	

	/*A trace de l'appel de procedure */
	va_text = "--> XZAC41_Lire_Commande_PAL"
	COM01_Trace(0,va_text)


	/*A Definition de vl_parametres */
	vl_parametres[0].type	= SYB#INT4_
	vl_parametres[0].data	= va_NumPA
	vl_parametres[0].output	= FALSE

	vl_parametres[1].type	= SYB#INT1_
	vl_parametres[1].data	= va_Site
	vl_parametres[1].output	= FALSE

	vl_parametres[2].type	= SYB#INT4_
	vl_parametres[2].data	= 0			' NumEqt
	vl_parametres[2].output	= TRUE

	vl_parametres[3].type	= SYB#CHAR_
	vl_parametres[3].data	= 0			' Bandeau
	vl_parametres[3].output	= TRUE

	vl_parametres[4].type	= SYB#CHAR_
	vl_parametres[4].data	= 0			' Vitesse
	vl_parametres[4].output	= TRUE

	vl_parametres[5].type	= SYB#DATETIME_
	vl_parametres[5].data	= 0			' heure lancement
	vl_parametres[5].output	= TRUE

	vl_parametres[6].type	= SYB#DATETIME_
	vl_parametres[6].data	= 0			' heure fin lancement
	vl_parametres[6].output	= TRUE

	vl_parametres[7].type	= SYB#DATETIME_
	vl_parametres[7].data	= 0			' heure succes
	vl_parametres[7].output	= TRUE

	vl_parametres[8].type	= SYB#DATETIME_
	vl_parametres[8].data	= 0			' heure echec
	vl_parametres[8].output	= TRUE

	vl_parametres[9].type	= SYB#DATETIME_
	vl_parametres[9].data	= 0			' heure fin
	vl_parametres[9].output	= TRUE

	
	vl_retour[0] = COM_NOK
	
	/*A Execution de la procedure XZAC805_Lire_Commande_PRV */
	IF COM05_SQL_Procedure (XZAC805_Lire_Commande_PRV,
				vl_parametres,vl_resultats,C_MODULE) <> COM_OK
	{
		RETURN (vl_retour)

	}

	/*A MAJ de la table PRV avec les arguments de sorties */
	vl_retour[0]			=	vl_resultats.return_parameters[0]+0	'Num Eqt
	vl_retour[1]			=	vl_resultats.return_parameters[1]	'Bandeau
	vl_retour[2]			=	vl_resultats.return_parameters[2]	'Vitesse
	vl_retour[3]			=	0									'Num Evt
	vl_retour[4]			=	0									'Cle Evt
	vl_retour[5]			=	0									'Priorite
	vl_retour[6]			=	XDC_DISTANCE_INCONNUE				'Distance Evt
	vl_retour[7]			=	va_NumPA							'Num Action


	if LEN@(vl_resultats.return_parameters[5]) 
			<= LEN@(vl_resultats.return_parameters[6])
	{	
		vl_retour[8] = "Horodate Echec : " ++ COM18_Date_SGBD_Formatee(vl_resultats.return_parameters[6])
	}

	if LEN@(vl_resultats.return_parameters[6]) 
		<= LEN@(vl_resultats.return_parameters[5])
	{
		vl_retour[8] = "Horodate Succes : " ++ COM18_Date_SGBD_Formatee(vl_resultats.return_parameters[5])
	}

	vl_retour[9]  = "Horodate Fin : " ++ COM18_Date_SGBD_Formatee(vl_resultats.return_parameters[7])
	

	RETURN (vl_retour)

ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Retourne un message a l'operateur et
*	 					trace l'acces interdit a une macro.
*
* --------------------------------------------------------------------*/

MACRO ITMA_TPRV_Acces_interdit(va_NomMacro,va_NomOperateur)

/*----------------------------------------------------------------------
* ARGUMENTS EN ENTREE :
* 
*	va_NomMacro : le nom de la macro interdite
*	va_NomOperateur : Nom de l'operateur
*
* ARGUMENTS EN SORTIE : aucun
*
* CODE RETOUR	: 

* CONDITION D'UTILISATION :
*   appel d'une macro avec les droits d'acces insuffisant.
*
* FONCTION :
*	Retourne un message a l'operateur et trace l'acces interdit
*	a une macro
*
-------------------------------------------------------------------- */

	/*A message d'info a l'operateur */

	var	vl_tptext			' zone de text tampon
	
	if NOT (SYSTEM_VAR@ (vg_formation) <> NULL)
	{
		vl_tptext = "L'utilisateur " ++va_NomOperateur ++ 
		" n'a pas les droits suffisants" ++ NUM_TO_STRING@(10) ++
						"pour la fonction : " ++ va_NomMacro
		info_message@(vl_tptext)

		/*A trace de l'appel de procedure */
		vl_tptext = " Acces Macro "
				++ va_NomMacro ++ " refuse pour " ++ va_NomOperateur
		COM01_Trace(0,vl_tptext)
	}

ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Lecture de l'etat d'un PRV a une horodate donnee
*
*  ---------------------------------------------------------------------
*/

MACRO ITMA_TPRV_Lecture_Etat_PRV (va_mode_formation,
				 FORMAT  PA_PRV va_PRV,va_maj_etat,
				 va_module)


/*
* ARGUMENTS EN ENTREE 	:
*   aucun
*
*
* ARGUMENTS EN SORTIE	: 
*	va_PRV	: Etat d'affichage du PRV
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_PRV
*
*
* FONCTION
*   Appel XZAT51_Utilisation_PRV.
*
---------------------------------------------------------------------------- */


	VAR	vl_lib_fmc		' libelle fmc
	var	vl_tptext			' zone de text tampon
	VAR	vl_horodate
	var	vl_Heure			' heure courante base

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats
	
	vl_Heure = COM09_Date_Courante()

	/*A Definition de vl_parametres */
	vl_parametres[0].type	= SYB#DATETIME_
	if NOT va_mode_formation
		vl_parametres[0].data	= NULL
	else
	{
		vl_horodate = SYSTEM_VAR@(vg_Horodate_Formation)
		vl_parametres[0].data	= vl_horodate
	}
	vl_parametres[0].output	= FALSE

	vl_parametres[1].type	= SYB#INT1_
	vl_parametres[1].data	= va_PRV.NumEqt+0
	vl_parametres[1].output	= FALSE

	vl_parametres[2].type	= SYB#INT2_
	vl_parametres[2].data	= 0			' Dispo PRV
	vl_parametres[2].output	= TRUE

	vl_parametres[3].type	= SYB#CHAR_
	vl_parametres[3].data	= 0			' picto
	vl_parametres[3].output	= TRUE

	vl_parametres[4].type	= SYB#CHAR_
	vl_parametres[4].data	= 0			' bandeau
	vl_parametres[4].output	= TRUE

	vl_parametres[5].type	= SYB#INT4_
	vl_parametres[5].data	= 0			' Num Evt
	vl_parametres[5].output	= TRUE

	vl_parametres[6].type	= SYB#INT2_
	vl_parametres[6].data	= 0			' Cle Evt
	vl_parametres[6].output	= TRUE

	vl_parametres[7].type	= SYB#INT2_
	vl_parametres[7].data	= 0			' Priorite
	vl_parametres[7].output	= TRUE

	vl_parametres[8].type	= SYB#INT4_
	vl_parametres[8].data	= 0			' Distance Evt
	vl_parametres[8].output	= TRUE

	vl_parametres[9].type	= SYB#INT4_
	vl_parametres[9].data	= 0			' Num Action
	vl_parametres[9].output	= TRUE

	vl_parametres[10].type	= SYB#INT1_
	vl_parametres[10].data	= 0			' Num Site Action
	vl_parametres[10].output	= TRUE

	/*A Execution de la procedure XZAT05_Utilisation_PRV */
	IF COM05_SQL_Procedure 
			(XZAT51_Utilisation_PRV,vl_parametres,vl_resultats,va_module)
			 <> COM_OK
    			RETURN (COM_NOK)

	/*A MAJ de la table PRV avec les arguments de sorties */
	va_PRV.DispoPRV 			=	vl_resultats.return_parameters[0]
	/*A si va_PRV.NumeroAction : MAJ avec lecture base */
	if (va_maj_etat) 
	{
		va_PRV.Vitesse	= SUBSTRING@(vl_resultats.return_parameters[3],1,15)
	  	va_PRV.Bandeau 	= SUBSTRING@(vl_resultats.return_parameters[4],1,15)
	}

	/*A MAJ avec base du num Evt + Cle & NumAction */
	va_PRV.NumEvt		=	vl_resultats.return_parameters[5]+0
	va_PRV.CleEvt			=	vl_resultats.return_parameters[6]+0
	va_PRV.Priorite		=	vl_resultats.return_parameters[7]
	va_PRV.DistanceEvt		=	vl_resultats.return_parameters[8]
	va_PRV.NumeroAction		=	vl_resultats.return_parameters[9]

	if va_PRV.NumEvt<>0 and  va_PRV.CleEvt<>0
	{
		/*A MAJ du libelle fmc */
		ITMA_COM_Lire_Evenement ( 
					va_PRV.NumEvt,
					va_PRV.CleEvt, 
					vl_lib_fmc, 
					va_module)
		va_PRV.fmc_liee = COM11_Libelle_FMC(vl_lib_fmc)
	}
	else	va_PRV.fmc_liee = "Pas de libelle F.M.C"

RETURN (COM_OK)

ENDMACRO


/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Lecture de la configuration des PAL
*
*  ---------------------------------------------------------------------
*/

MACRO ITMA_TPRV_Init_Config_PRV ( FORMAT Config_PRV va_liste_PRV )

/*
* ARGUMENTS EN ENTREE : 
*		aucun
*
* ARGUMENTS EN SORTIE :
* 
*		va_liste_PAL 		: liste des PAL
*		va_liste_scenario 	: liste des scenario par type
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPP
*
*
* FONCTION
*   	Lecture de la configuration des PAL.
*
--------------------------------------------------------------------- */

VAR	vl_liste_indice
VAR	vl_machine_LT
VAR 	vl_liste
VAR	i,j, vl_nb_eqt
VAR	vl_Index_Site
VAR	FORMAT	COM_Localisation 		vl_localisation
VAR	FORMAT 	COM_Donnees_Equipements 	vl_info_equip
VAR	FORMAT 	COM_Donnees_Equipements 	vl_info_equip_suite
VAR FORMAT 	TDO_District			vl_les_districts
VAR vl_distict_concerne

VAR FORMAT SQL_Procedure_Params@ vl_parametres
VAR FORMAT SQL_Procedure_Result@ vl_resultats

va_liste_PRV 		= SYSTEM_VAR@ (vg_Config_PRV)

/*A Si les liste PRV sont deja a jour alors retourne */
if ( va_liste_PRV <> NULL ) 
{
   	RETURN (COM_OK)
}


/*A
 * Listes des types de PRV
 * -----------------------------------
 */

/*A MAJ de vl_parametres */
vl_parametres[0].type	= SYB#CHAR_
vl_parametres[0].data	= XDC_BASE_CFG			' Nom de la base
vl_parametres[0].output	= FALSE

vl_parametres[1].type	= SYB#CHAR_
vl_parametres[1].data	= XDC_CHAINE_VIDE		' Nom de la machine
vl_parametres[1].output	= FALSE

/*A Execution de la procedure XZAO870_Lire_Liste_PRV */			
IF COM05_SQL_Procedure
	( XZAO870_Lire_Liste_PRV, vl_parametres, vl_resultats ,C_MODULE) <> COM_OK
	RETURN (COM_NOK)

vl_liste = vl_resultats.select_results[0]
/*A si le resultat du select n'est pas nul */
if ( ARRAY_SIZE@(vl_liste)>0 )
{
	/*A pour chaque enregistrement */
	for  i=0 to ARRAY_SIZE@(vl_liste)-1
		/* Mettre a jour le numero du type de PRV */
		va_liste_PRV [vl_liste[i,0]].NumType = vl_liste[i,1]
	next i
}

/*!!!!! */
/*A
 * Listes des PRV
 * -----------------------------------
 */

ITMA_COM_Lire_Equipements (	XDC_EQT_PRV, 
						NULL,
				 	 	XDC_EQT_HS,		' Conditions niees
				 	 	XDC_EQT_NON_HS, 	' Conditions 
				 	 	0,				' Exclusions
					 	0,
					 	vl_info_equip,
					 	C_MODULE )


/*A si le resultat du select n'est pas nul */

vl_les_districts = SYSTEM_VAR@ (vg_les_districts)
/*A si le resultat du select n'est pas nul */
if ARRAY_SIZE@(vl_info_equip)>0 
{
	/*A pour chaque enregistrement */
	for  i=0 to ARRAY_SIZE@(vl_info_equip)-1

		vl_localisation.NumAuto 	= vl_info_equip[i].autoroute

		vl_localisation.PR 		= vl_info_equip[i].PR

		vl_localisation.sens_circulation = vl_info_equip[i].sens
		va_liste_PRV [vl_info_equip[i].numero].Identifiant =
					"PAL" ++ COM10_Localisation(vl_localisation)

		va_liste_PRV [vl_info_equip[i].numero].Autoroute =  
								vl_info_equip[i].autoroute

		va_liste_PRV [vl_info_equip[i].numero].PR =  
									vl_info_equip[i].PR

		va_liste_PRV [vl_info_equip[i].numero].Sens =  
									vl_info_equip[i].Sens

		va_liste_PRV [vl_info_equip[i].numero].SiteGestion = vl_info_equip[i].SiteGestion

		vl_Index_Site = ARRAY_INDEX@ (ARRAY_COLUMN@ 
				(vl_les_districts,TDO_NUMERO_DISTRICT),
					ITMA_COM_Site_PR(vl_localisation.NumAuto,
								vl_localisation.PR,C_MODULE))
		if vl_Index_Site >= 0
		{
			va_liste_PRV [vl_info_equip[i].numero].NumSite = 
						vl_les_districts[vl_Index_Site].numero
			va_liste_PRV [vl_info_equip[i].numero].NomSite = 
							vl_les_districts[vl_Index_Site].code
		}
		ITMA_TPRV_Nom_Machine_LT(vl_info_equip[i].numero,
				vl_info_equip[i].type,	vl_machine_LT)

		va_liste_PRV[vl_info_equip[i].numero].nom_machine_LT=
  										vl_machine_LT

	next i

}


RETURN (COM_OK)

ENDMACRO



/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Lecture du nom de machine LT d'un equipement 
*
*  ---------------------------------------------------------------------
*/

MACRO ITMA_TPRV_Nom_Machine_LT(va_NumEqt,va_TypeEqt,va_machine_LT)

/*
* ARGUMENTS EN ENTREE : 
*
*		va_NumEqt		:	le numero d'equipement
*		va_machine_LT	:	le nom de la machine du LT
*
* ARGUMENTS EN SORTIE : 
*		NomMachineLT	: Nom de la machine LT
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_
*
*
* FONCTION
*   	Lecture Du Nom du LT de l'equipement
*
--------------------------------------------------------------------- */

	VAR FORMAT SQL_Procedure_Params@ vl_parametres
	VAR FORMAT SQL_Procedure_Result@ vl_resultats

	var	i,j				' index de loop
	var vl_text

	vl_parametres[0].type	= SYB#INT4_
	vl_parametres[0].data	= va_NumEqt			' numero equipement
	vl_parametres[0].output	= FALSE
	
	vl_parametres[1].type	= SYB#INT4_
	vl_parametres[1].data	= va_TypeEqt			' type equipement	
	vl_parametres[1].output	= FALSE

	vl_parametres[2].type	= SYB#CHAR_			' NomMachineLT
	vl_parametres[2].data	= 0
	vl_parametres[2].output	= TRUE


	IF COM05_SQL_Procedure (XZAO05_Nom_Machine_LT,
					vl_parametres,vl_resultats,C_MODULE) <> COM_OK
	{
		RETURN (COM_NOK)
	}
	/*A recuperation du nom de la machine LT */
	va_machine_LT = SUBSTRING@(vl_resultats.return_parameters[0],1,7)
	
	/*B trace du nom de machine LT */
	vl_text = " Pilotage PRV " ++ va_NumEqt ++ " - Nom machine LT "
										 ++ va_machine_LT 
	COM01_Trace(0,vl_text)
	COM01_Trace(0," ")

	RETURN (COM_OK)

ENDMACRO



/*X*/
/* ---------------------------------------------------------------------
* SERVICE RENDU :	Lecture des equipements disponibles
*
*  ---------------------------------------------------------------------
*/

MACRO ITMA_TPRV_Liste_Eqt_Disponibles( va_District, 
						FORMAT Config_PRV va_Config_PRV )

/*
* ARGUMENTS EN ENTREE :
* 
*		va_District : le numero du district
*
* ARGUMENTS EN SORTIE :
* 
*		ListeEqts		: la liste des equipements correspondants
*
*
* CODE RETOUR		: COM_OK en cas d'execution correcte, COM_NOK sinon.
*
*
* CONDITION D'UTILISATION
*   ITMA_TPRV
*
*
* FONCTION
*   	Lecture des equipements en disponibilites.
*
--------------------------------------------------------------------- */

	var	FORMAT COM_Donnees_Equipements vl_info_equip
	var	FORMAT COM_Donnees_Equipements vl_info_equip_suite
	var	vl_Liste		' liste formattee
	var	i, k			' index de loop
	var vl_district
	var vl_nb_eqt

	if va_District = XDC_CI

	{	vl_District=NULL }
	else
	{	vl_District=va_District }

	ITMA_COM_Lire_Equipements(	XDC_EQT_PRV, 
								vl_District,
								XDC_EQT_MINEUR, 	' Conditions niees
								XDC_EQT_MINEUR,		' Conditions
				 				XDC_EQT_HS + XDC_EQT_MAJEUR + XDC_EQT_CRITIQUE + XDC_EQT_DESACTIVE + XDC_EQT_INHIBE,	' Exclusions	
								0,
								vl_info_equip,
					     		C_MODULE )

	/*A RAZ indicateurs de disponibilite de l'equipement */
	for i=0 to ARRAY_SIZE@(va_Config_PRV)
		va_Config_PRV[i].Disponible=FALSE
	next i

	/*A Construction de la liste des equipements */
	if  IS_ARRAY@(vl_info_equip)
	{  
		for i = 0 to ARRAY_SIZE@(vl_info_equip) - 1	
	   		if (vl_info_equip[i].sitegestion = va_District) OR
				((va_District = XDC_CI) and  
	 	  			 (ITMA_COM_District_pilotable(
						vl_info_equip[i].sitegestion, 
							XDC_LIB_PAL,FALSE,NULL)) )
			{
				vl_Liste[k] = va_Config_PRV[vl_info_equip[i].numero].Identifiant

				va_Config_PRV[vl_info_equip[i].numero].Disponible=TRUE
				k= k + 1
			}

		next i
	}
	else
	{
		vl_Liste[0]		= 	""
		info_message@("Pas de PRV diponible dans cette région ...")
	}

	RETURN(vl_Liste)

ENDMACRO



