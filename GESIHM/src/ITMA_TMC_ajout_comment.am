/*E*/
/*Fichier : $Id: ITMA_TMC_ajout_comment.am,v 1.16 2005/03/02 11:53:07 gesconf Exp $      Release : $Revision: 1.16 $        Date : $Date: 2005/03/02 11:53:07 $
------------------------------------------------------
* STERIA *  PROJET MIGRAZUR
------------------------------------------------------
* SOUS-SYSTEME  GESIHM
------------------------------------------------------
* MODULE MTMC * FICHIER ITMA_TMC_ajout_comment.am
------------------------------------------------------
* DESCRIPTION DU MODULE :
*
* Ajout de commentaire a une fmc
*
*
------------------------------------------------------
* HISTORIQUE :
*
* torregrossa 15/06/95 : mise a jour du numero de commentaire  (V 1.10)
* Torregrossa 15 Nov 1995 : Modif horodate courante pour Applix version 4.1 V 1.12
* Torregrossa 08 Jan 1996 : Ajout controle longueur commentaire V 1.13
* Torregrossa 30 Jan 1996 : ajout des secondes aux commentaires V1.15
* Hilmarcher  17/02/2005  : Ajout bouton de correction orthographique DEM422 v1.16 
------------------------------------------------------*/

include	"dbase_.am"
include 	"../inc/ITMA_COM.h"
include 	"../inc/ITMA_TDO.h"
include	"../../XDMICG/inc/xdc_ax.h"
include	"../inc/ITMA_TAR.h"
include	"../../XDMICG/inc/xzic_ax.h"
include	"../inc/ITMA_TMC.h"


define MODE_CREATION 0
define MODE_MODIF	1
define C_MODULE	"ITMA_TMC_ajout_comment"
	


/*X*/
/*------------------------------------------------------
* SERVICE RENDU : 
* ajoute un commentaire a la liste des commentaires
*
------------------------------------------------------
* SEQUENCE D'APPEL :
*/

MACRO ITMA_TMC_ajout_comment(FORMAT TMC_comment ta_comment,FORMAT COM_Identifiant_FMC ta_num_evt,va_mode)

/*
* ARGUMENTS EN ENTREE :
*
*
* ARGUMENTS EN SORTIE :
* nouveau commentaire
*
* CODE RETOUR : 
* aucun
*
*
*
---------------------------------------------------------*/
VAR vl_fenetre,vl_evt, vl_la_fenetre_est_active
VAR    vl_installer_traitement_erreur
VAR FORMAT TMC_comment tl_comment,vl_comment
VAR vl_indice,tl_texte
VAR vl_horodate
VAR vl_texte

/*A
 * Traitements en cas d'erreur durant l'initialisation :
 * informer l'operateur, tracer l'erreur et abandonner
 * -----------------------------------------------------
 */

	ON ERROR {
   	 ERROR_BOX@
    	COM01_Trace (COM_WARNING, COM03_Message_Erreur (C_MODULE))
    	RETURN
}

	/*A on memorise les commentaires actuels*/
	tl_comment=ta_comment

	/*A affichage de la fenetre*/
	vl_fenetre=DB_LOAD@("ITMA_TMC_ajout_comment")
	DB_XPOS@(vl_fenetre,517)
	DB_YPOS@(vl_fenetre,677)

/*A
 * Traitements en cas d'erreur durant la visualisation :
 * informer l'operateur et selon la nature et la gravite
 * de l'erreur la tracer ou non, continuer ou abandonner
 * -----------------------------------------------------
 */

	vl_installer_traitement_erreur = TRUE
	WHILE vl_installer_traitement_erreur
   		 vl_installer_traitement_erreur = FALSE

    		ON ERROR {
        	ERROR_BOX@
     	   vl_installer_traitement_erreur = TRUE
    		}
	WEND

	vl_la_fenetre_est_active = TRUE

	WHILE vl_la_fenetre_est_active
		DB_DISPLAY_ONLY@(vl_fenetre, FALSE)
		DB_DISPLAY@(vl_fenetre)
		DB_DISPLAY_ONLY@(vl_fenetre, TRUE)

		/*A attente d'un evenement*/
		vl_evt=DB_EXIT_CTRL@(vl_fenetre)

		/*A suivant l'evenement*/
		CASE OF vl_evt
			/*A bouton annuler*/
			CASE "BP_annuler"
				RETURN (tl_comment)

			/*A Bouton correction orthographe DEM422*/
			CASE "BP_Verif"
				tl_texte = DB_EDITBOX_GET_DATA@(vl_fenetre,"BE_ajoutcomment")
				PEND_FOR_NEW_TASK@("ITMA_TMC_VerifOrtho",tl_texte)
				tl_texte=READ_ASCII_FILE@("/tmp/ajout_commentaire.txt","r")
				DB_EDITBOX_SET_DATA@(vl_fenetre,"BE_ajoutcomment",tl_texte)
			
			/*A bouton valider*/
			CASE "BP_valider"
				/*A recupere l'heure courante et la formate*/
				vl_horodate=COM09_Date_Courante(TRUE)

				/*A recupere le texte*/
				tl_texte=DB_EDITBOX_GET_DATA@(vl_fenetre,"BE_ajoutcomment")

				IF (IS_NULL@(tl_texte))
				{
					vl_comment.texte=null
					vl_la_fenetre_est_active = FALSE
					RETURN(ta_comment)
				}
				ELSE
				{
					vl_comment.texte=ARRAY_TO_STRING@(tl_texte,MTAR_CAR_SEPAR)

					IF LEN@(vl_comment.texte) <= COM_LG_TXT
					{
						vl_la_fenetre_est_active = FALSE

						vl_comment.fin=XDC_DATE_NULLE
						vl_comment.numero=null
						vl_comment.validation=vl_horodate
						IF (SYSTEM_VAR@(vg_surcharge)=TRUE)
							vl_comment.site=SYSTEM_VAR@(vg_site_surcharge)
						ELSE
							vl_comment.site=SYSTEM_VAR@(vg_site)
	
						/*A sauvegarde en base si le mode n'est pas creation*/
						IF (va_mode<>MODE_CREATION)
							IF (ITMA_TMC_valide_comment(vl_comment,
										vl_horodate,
										ta_num_evt)<>COM_OK)
							RETURN (ta_comment)
	
						/*A numero du commentaire */
						vl_comment.numero=ARRAY_SIZE@(ta_comment)+1
	
						/*A ajout du commentaire a la liste existante*/
						tl_comment[ARRAY_SIZE@(ta_comment)]=vl_comment
		
						/*A renvoie la nouvelle liste*/
						RETURN(tl_comment)
					}
					ELSE	INFO_MESSAGE@("Le commentaire est trop long (250 caractères maximum)")
				}
		ENDCASE
	WEND
ENDMACRO

